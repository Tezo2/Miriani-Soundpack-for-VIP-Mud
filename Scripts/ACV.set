; Configuration
;
; Should the ACV move up and down by using the direction prompt?
#Var ACVPromptNavig 0

#Var AcvCoordX {}
#Var AcvCoordY {}
#Var AcvCoordZ {}

#Alias CalculateRelativeNumericDir {
 #if {%IsNumber(%1)=0 OR %IsNumber(%2)=0 OR %IsNumber(%3)=0 OR %IsNumber(%4)=0 OR %IsNumber(%5)=0 OR %IsNumber(%6)=0} {
  #Var RelativeNumericDir {%1 %2 %3}
 } {
  #Var RelativeNumericDir {};
  #Var RelativeNumericDirPrefix {};
  #Math RelativeNumericTmpX {%max(%1,%4) - %min(%1,%4)};
  #Math RelativeNumericTmpY {%max(%2,%5) - %min(%2,%5)};
  #Math RelativeNumericTmpZ {%max(%3,%6) - %min(%3,%6)};
  #If {(@RelativeNumericTmpX>1 OR @RelativeNumericTmpY>1 OR @RelativeNumericTmpZ>1) OR (@RelativeNumericTmpX=0 AND @RelativeNumericTmpY=0 AND @RelativeNumericTmpZ=0)} {
   #if {@RelativeNumericTmpX>1 OR @RelativeNumericTmpY>1 OR @RelativeNumericTmpZ>1} {
    #if {@RelativeNumericTmpX>0} {
     #var RelativeNumericTmpXDir {@RelativeNumericTmpX%if(%4 > %1,W,E)};
     #If {@RelativeNumericTmpY>0 OR @RelativeNumericTmpZ>0} {
     #var RelativeNumericTmpXDir {@RelativeNumericTmpXDir, }
     }
    } {
     #var RelativeNumericTmpXDir {}
    };
    #if {@RelativeNumericTmpY>0} {
     #var RelativeNumericTmpYDir {@RelativeNumericTmpY%if(%5 > %2,N,S)};
     #If {@RelativeNumericTmpZ>0} {
     #var RelativeNumericTmpYDir {@RelativeNumericTmpYDir, }
     }
    } {
     #var RelativeNumericTmpYDir {}
    };
    #if {@RelativeNumericTmpZ>0} {
     #var RelativeNumericTmpZDir {@RelativeNumericTmpZ%if(%6 > %3,U,D)}
    } {
     #var RelativeNumericTmpZDir {}
    };
    #var RelativeNumericDir {@RelativeNumericTmpXDir@RelativeNumericTmpYDir@RelativeNumericTmpZDir}
   } {
    #var RelativeNumericDir {C}
   }
  } {
   #if {%3>%6} {
    #var RelativeNumericDirPrefix {-}
   };
   #if {%3<%6} {
    #var RelativeNumericDirPrefix {+}
   };
   #if {%1=%4 AND %2=%5} {
    #var RelativeNumericDir 5
   } {
    #if {%1=%4} {
     #if {%2>%5} {
      #var RelativeNumericDir 2
     } {
      #var RelativeNumericDir 8
     }
    } {
     #if {%2=%5} {
      #if {%1>%4} {
       #var RelativeNumericDir 6
      } {
       #var RelativeNumericDir 4
      }
     } {
      #if {%1>%4} {
       #if {%2>%5} {
        #var RelativeNumericDir 3
       } {
        #var RelativeNumericDir 9
       }
      } {
       #if {%2>%5} {
        #var RelativeNumericDir 1
       } {
        #var RelativeNumericDir 7
       }
      }
     }
    }
   };
   #var RelativeNumericDir {@RelativeNumericDirPrefix@RelativeNumericDir}
  }
 }
} {}


#Alias ACVMove {
 #If {%IsNumber(%Left(%1,1))=1} {
  #Var ACVTmp1 {%1};
  #Var ACVTmp2 {}
 } {
  #Var ACVTmp1 {%Right(%1,1)};
  #Var ACVTmp2 {%Left(%1,1)}
 };
 #If {@ACVNextMoveXY=""} {#Var ACVNextMoveXY {@ACVTmp1}} {#Var ACVNextMoveXY {@ACVNextMoveXY|@ACVTmp1}};
 #If {@ACVNextMoveZ=""} {#Var ACVNextMoveZ {@ACVTmp2}} {#Var ACVNextMoveZ {@ACVNextMoveZ|@ACVTmp2}};
 #If {@ACVTmp2<>""} {
  #If {@ACVPromptNavig=0} {
   #Exec {>@ACVTmp2@ACVTmp1}
  } {
   #Exec {>@ACVTmp2};
   #Exec {>@ACVTmp1}
  }
 } {
  #Exec {>@ACVTmp1}
 }
} {}
; Use the following code for all occurrences when your movement command fails:
#Trigger {This vehicle cannot land here.} {
 %Pop(ACVNextMoveXY);
 %Pop(ACVNextMoveZ)
} {Main}

#Trigger {Weapon launch sequence initiated. Bomb's away!} {
 #GagLine 1 All;
 msplay ACV\Bomb*4@Ext
} {Main|Vehicle}
#Trigger {The vehicle * pelted by automatic weapon fire from *} {
 #If {@MissionMode=1} {#GagLine 1 Voice};
 #Sub {Hit by %2};
 mwplay Ship\Combat\YouHit*11@Ext
} {Main|Vehicle}
#Trigger {Current coordinates: &{AcvCoordX}, &{AcvCoordY}, &{AcvCoordZ}} {
 #Sub {@AcvCoordX @AcvCoordY @AcvCoordZ}
} {Main|Vehicle|ACVMoveMode}
#Trigger {Coordinates: *, *, *} {
 #Var AcvObjectX %1;
 #Var AcvObjectY %2;
 #Var AcvObjectZ %3;
 #If {@ModifyAcvOutput=1} {
  CalculateRelativeNumericDir @AcvObjectX @AcvObjectY @AcvObjectZ @AcvCoordX @AcvCoordY @AcvCoordZ;
  #Sub {@RelativeNumericDir}
 }
} {Main|Vehicle|ACVMoveMode}
#Trigger {* Airspace; Grid *, *} {
 #GagLine 1 Voice;
 #Class ACVAirspace Enable
} {Main|Vehicle}
#Trigger {Current Coordinates: &{ACVCoordX}, &{ACVCoordY}, &{ACVCoordZ}} {
 #GagLine 1 All;
 #Class ACVAirspace Disable
} {Main|Vehicle|ACVAirspace}
#Trigger {You are suddenly pressed against your seat as the vehicle is catapulted out of the docking bay and into space. *} {
 #GagLine 1 All;
 msplay ACV\ACVLaunch@Ext;
 msplay Ship\Move\Subwarp@Ext
} {Main|Vehicle}
