; Configuration
;
; Should the ACV move up and down by using the direction prompt?
#Var ACVPromptNavig 0

#Var RelativeNumericDir {Nothing.}
#Var RelativeNumericDirPrefix {}
#Var AcvCoordX {}
#Var AcvCoordY {}
#Var AcvCoordZ {}

; Syntax: CalculateRelativeNumericDir TargetX TargetY TargetZ CurrentX CurrentY CurrentZ
#Alias CalculateRelativeNumericDir {
 #If {%IsNumber(%1)=0 OR %IsNumber(%2)=0 OR %IsNumber(%3)=0 OR %IsNumber(%4)=0 OR %IsNumber(%5)=0 OR %IsNumber(%6)=0} {
  #Var RelativeNumericDir {%1 %2 %3}
 } {
  #Var RelativeNumericDir {};
  #Var RelativeNumericDirPrefix {};
  #Math RelativeNumericTmpX {%max(%1,%4) - %min(%1,%4)};
  #Math RelativeNumericTmpY {%max(%2,%5) - %min(%2,%5)};
  #Math RelativeNumericTmpZ {%max(%3,%6) - %min(%3,%6)};
  #If {(@RelativeNumericTmpX>1 OR @RelativeNumericTmpY>1 OR @RelativeNumericTmpZ>1) OR (@RelativeNumericTmpX=0 AND @RelativeNumericTmpY=0 AND @RelativeNumericTmpZ=0)} {
   #If {@RelativeNumericTmpX>1 OR @RelativeNumericTmpY>1 OR @RelativeNumericTmpZ>1} {
    #If {@RelativeNumericTmpX>0} {
     #Var RelativeNumericTmpXDir {@RelativeNumericTmpX%if(%4 > %1,W,E)};
     #If {@RelativeNumericTmpY>0 OR @RelativeNumericTmpZ>0} {
     #Var RelativeNumericTmpXDir {@RelativeNumericTmpXDir, }
     }
    } {
     #Var RelativeNumericTmpXDir {}
    };
    #If {@RelativeNumericTmpY>0} {
     #Var RelativeNumericTmpYDir {@RelativeNumericTmpY%if(%5 > %2,N,S)};
     #If {@RelativeNumericTmpZ>0} {
     #Var RelativeNumericTmpYDir {@RelativeNumericTmpYDir, }
     }
    } {
     #Var RelativeNumericTmpYDir {}
    };
    #If {@RelativeNumericTmpZ>0} {
     #Var RelativeNumericTmpZDir {@RelativeNumericTmpZ%if(%6 > %3,U,D)}
    } {
     #Var RelativeNumericTmpZDir {}
    };
    #Var RelativeNumericDir {@RelativeNumericTmpXDir@RelativeNumericTmpYDir@RelativeNumericTmpZDir}
   } {
    #Var RelativeNumericDir {C}
   }
  } {
   #If {%3>%6} {
    #Var RelativeNumericDirPrefix {Minus }
   };
   #If {%3<%6} {
    #Var RelativeNumericDirPrefix {Plus }
   };
   #If {%1=%4 AND %2=%5} {
    #Var RelativeNumericDir 5
   } {
    #If {%1=%4} {
     #If {%2>%5} {
      #Var RelativeNumericDir 2
     } {
      #Var RelativeNumericDir 8
     }
    } {
     #If {%2=%5} {
      #If {%1>%4} {
       #Var RelativeNumericDir 6
      } {
       #Var RelativeNumericDir 4
      }
     } {
      #If {%1>%4} {
       #If {%2>%5} {
        #Var RelativeNumericDir 3
       } {
        #Var RelativeNumericDir 9
       }
      } {
       #If {%2>%5} {
        #Var RelativeNumericDir 1
       } {
        #Var RelativeNumericDir 7
       }
      }
     }
    }
   };
   #Var RelativeNumericDir {@RelativeNumericDirPrefix@RelativeNumericDir}
  }
 }
} {}

#Alias ACVMove {
 #If {%IsNumber(%Left(%1,1))=1} {
  #Var ACVTmp1 {%1};
  #Var ACVTmp2 {}
 } {
  #Var ACVTmp1 {%Right(%1,1)};
  #Var ACVTmp2 {%Left(%1,1)}
 };
 #If {@ACVNextMoveXY=""} {#Var ACVNextMoveXY {@ACVTmp1}} {#Var ACVNextMoveXY {@ACVNextMoveXY|@ACVTmp1}};
 #If {@ACVNextMoveZ=""} {#Var ACVNextMoveZ {@ACVTmp2}} {#Var ACVNextMoveZ {@ACVNextMoveZ|@ACVTmp2}};
 #If {@ACVTmp2<>""} {
  #If {@ACVPromptNavig=0} {
   #Exec {>@ACVTmp2@ACVTmp1}
  } {
   #Exec {>@ACVTmp2};
   #Exec {>@ACVTmp1}
  }
 } {
  #Exec {>@ACVTmp1}
 }
} {}
; Use the following code for all occurrences when your movement command fails:
#Trigger {This vehicle cannot land here.} {
 %Pop(ACVNextMoveXY);
 %Pop(ACVNextMoveZ)
} {Main}

#Trigger {Weapon launch sequence initiated. Bomb's away!} {ExpPipe {%0} {Bomb's away!} {};
#sub @ExpString;
msplay ACV\Bomb*4@Ext} {Main}
#Trigger {The vehicle * pelted by automatic weapon fire from *.} {
 ExpPipe {%0} {%0} {Hit by %2};
 #If {@MissionMode=1} {#GagLine 1 Voice};
 #Sub @{ExpString};
 mwplay Ship\Combat\YouHit*11@Ext
} {Main}
#Trigger {Current coordinates: &{AcvCoordX}, &{AcvCoordY}, &{AcvCoordZ}} {
 #Sub {@AcvCoordX @AcvCoordY @AcvCoordZ}
} {Main|ACVMoveMode}
#Trigger {Coordinates: *, *, *} {
 #Var AcvObjectX %1;
 #Var AcvObjectY %2;
 #Var AcvObjectZ %3;
 #If {@ModifyAcvOutput=1} {
  CalculateRelativeNumericDir @AcvObjectX @AcvObjectY @AcvObjectZ @AcvCoordX @AcvCoordY @AcvCoordZ;
  #Sub {@RelativeNumericDir}
 }
} {Main|ACVMoveMode}
#Trigger {* Airspace; Grid *, *} {
 ExpPipe {%0} {[%1]: %2, %3} {};
 #Sub @ExpString;
 #Class ACVAirspace enable
} {Main}
#Trigger {Current Coordinates: &{ACVCoordX}, &{ACVCoordY}, &{ACVCoordZ}} {
 #gagline 1 all;
 #Class ACVAirspace disable
} {Main|ACVAirspace}
#Trigger {You are suddenly pressed against your seat as the vehicle is catapulted out of the docking bay and into space. *} {
 #gagline 1 all;
 msplay ACV\ACVLaunch@Ext;
 msplay Ship\Move\Subwarp@Ext
} {Main|Vehicle}
