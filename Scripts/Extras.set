#Var ExtraScriptFiles {
Cannons^Alerts you of how many shots you have remaining, and plays a sound after the last shot is fired
Dig^Shows you how many feet you have dug when hunting for archaeological artifacts
ArchaeologyHelper^Makes archaeology a bit easier
Babies^Baby sounds
Books^Makes reading books a bit easier (try turnpage after reading a page)
GagDig^Gives you the ability to gag people digging with the GagDig command
Blades^For those odd people with swords and stuff in a sci-fi game
ScanningEnhancements^Adds certain scanning enhancements
Typing^Allows you to type with only one hand, using the number pad
Development^Contains useful tools for soundpack development
}

#Alias LoadExtras {
 #ForAll {%Replace(@ExtraScriptFiles,%CRLF,|)} {
  #If {%Pos(^,%i)>0} {LoadExtra {%Word(%i,^,1)} {%1}}
 }
} {}

#Alias LoadExtra {
 #Var ExtraTmp1 {LoadExtraScript%1};
 #If {%Defined(@ExtraTmp1)=1} {
  #If {@{@ExtraTmp1}=1} {
   #If {@GagExtraLoads=0} {#Say {Loading the %1 extra.}};
   #Load {@ScriptPath\Extras\%1.set}
  } {
   #If {%IfWord(NoUnload,%2,|)=0} {
    #UnLoad {@ScriptPath\Extras\%1.set}
   }
  }
 };
 #UnVar ExtraTmp1
} {}

#Exec {LoadExtras {NoUnload}}
