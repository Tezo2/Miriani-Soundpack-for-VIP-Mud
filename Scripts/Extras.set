#Var ExtraScriptFiles {
Babies^Baby sounds
Cannons^Alerts you of how many shots you have remaining, and plays a sound after the last shot is fired
Development^Contains useful tools for soundpack development
Fishing^Adds sounds for fishing
Games^Sounds for games such as air hockey, skeeball, assault, etc
LaserTag^Sounds for Laser Tag
Shenanigans^Sounds for slime dispensers, blades, etc
Socials^Sounds for kitchen appliances, swimming, campfires, swings, showers, punching bags, and much more
Typing^Allows you to type with only one hand, using the number pad
}

#Alias LoadExtras {
 #ForAll {%Replace(@ExtraScriptFiles,%CRLF,|)} {
  #If {%Pos(^,%i)>0} {LoadExtra {%Word(%i,^,1)} {%1}}
 }
} {}

#Alias LoadExtra {
 #Var ExtraTmp1 {LoadExtraScript%1};
 #If {%Defined(@ExtraTmp1)=1} {
  #If {@{@ExtraTmp1}=1} {
   #If {%IfWord(Quiet,%2,|)=0} {#Say {Loading the %1 extra.}};
   #Load {@ScriptPath\Extras\%1.set}
  } {
   #If {%IfWord(NoUnload,%2,|)=0} {
    #If {%IfWord(Quiet,%2,|)=0} {#Say {Unloading the %1 extra.}};
    #UnLoad {@ScriptPath\Extras\%1.set}
   }
  }
 };
 #UnVar ExtraTmp1
} {}

#Exec {LoadExtras {NoUnload|Quiet}}
