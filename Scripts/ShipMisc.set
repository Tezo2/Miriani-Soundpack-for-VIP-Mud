#Var Contributors {}
#Var LargestContributionLength 0
#Trigger {You contribute * credit* to the owner of the ship.} {
 ExpPipe {%0} {%0} {You contribute %1 to the owner of @ZoneName.};
 #Sub {@ExpString};
 PushMessage Contributions {@ExpString}
} {Main|Starship}
#Trigger {You have received a contribution of * credit* from *.} {
 #Var ContribID {%Pos(|%3|,@Contributors)};
 #If {@ContribID=0} {
  #Math ContribID {%Length(@Contributors)+1};
  #If {%Length(@Contributors)=0} {#Var Contributors {|}};
  #Var Contributors {@Contributors%3|}
 };
 #Var ContribVar {ContribID@ContribID};
 #If {%Defined(@ContribVar)=0} {#Var {@ContribVar} {0|0.0|}};
 #Math ContribTimes {%Word(@{@ContribVar},|,1)+1};
 BigAdd {%Word(@{@ContribVar},|,2)} {%1};
 #Var {@ContribVar} {@ContribTimes|@BigAddResult|%Word(@{@ContribVar},|,3):%1};
 #Var ContribLength {%Pos(.,@BigAddResult)};
 #If {@ContribLength>@LargestContributionLength} {#Var LargestContributionLength {@ContribLength}};
 ExpPipe {%0} {%0} {You receive %1 from %3.};
 #Sub {@ExpString};
 PushMessage Contributions {@ExpString};
 MirianiSoundPlay {Standard} {General\Misc\Credits*4@Ext};
 #ForAll {ID|Length|Times|Var} {#UnVar {Contrib%i}}
} {Main|Starship}

#Alias Contribs {
 #If {%Length(@Contributors)=0} {
  #Say {You have received no contributions since last reset.};
  #Abort
 };
 #Var Contributions {};
 #ForAll {@Contributors} {
  #If {%Length(%i)>0} {
   #Var ContribTmpID {%Pos(|%i|,@Contributors)};
   #Var ContribTmpVar {ContribID@ContribTmpID};
   #Var ContribTmpAmount {%Word(@{@ContribTmpVar},|,2)};
   #While {%Pos(.,@ContribTmpAmount)<@LargestContributionLength} {
    #If {%Pos(~,,@ContribTmpAmount)=4 or %Pos(.,@ContribTmpAmount)=4} {
     #Var ContribTmpAmount {0,@ContribTmpAmount}
    } {
     #Var ContribTmpAmount {0@ContribTmpAmount}
    }
   };
   #Var Contributions {@Contributions|@ContribTmpAmount:%i};
   #If {%1="clear"} {#UnVar {@ContribTmpVar}}
  }
 };
 #Var Contributions {%Copy(@Contributions,2,%Eval(%Length(@Contributions)-1))};
 #Var Contributions {%Sort(@Contributions,1)};
 #Var ContribOutput {Contributors:};
 #ForAll {@Contributions} {
  #Var ContribTmpAmount {%Word(%i,:,1)};
  #While {%IfWord(%Left(@ContribTmpAmount,1),~, 0, )=1} {#Var ContribTmpAmount {%Copy(@ContribTmpAmount,2,%Eval(%Length(@ContribTmpAmount)-1))}};
  #Var ContribOutput {@ContribOutput%CRLF  %Word(%i,:,2): @ContribTmpAmount.}
 };
 #Say {@ContribOutput};
 #If {%1="clear"} {
  #Var Contributors {};
  #UnVar Contributions;
  #Say {Contrib data cleared.}
 } {
  #Say {Use CONTRIBS CLEAR to reset.}
 };
 #ForAll {Output|TmpAmount|TmpID|TmpVar} {#UnVar {Contrib%i}}
} {}
#Alias BigAdd {
 #Var BigAddInt {%Word(%1,.,1)};
 #Var BigAddInt {%Replace(@BigAddInt,~,,|)};
 #Var BigAddInt {%ReverseList(@BigAddInt)};
 #Var BigAddTmpInt {%Word(%2,.,1)};
 #Var BigAddTmpInt {%Replace(@BigAddTmpInt,~,,|)};
 #Var BigAddTmpInt {%ReverseList(@BigAddTmpInt)};
 #Math BigAddTmp1 {%Left(%Word(%1,.,2),2)+%Left(%Word(%2,.,2),2)};
 #If {@BigAddTmp1>99} {
  #Var BigAddCarry 1;
  #Math BigAddTmp1 {@BigAddTmp1-100}
 } {
  #Var BigAddCarry 0
 };
 #Var BigAddResult {@BigAddTmp1};
 #Var BigAddSep {.};
 #Exec {#%Max(%NumWords(@BigAddInt,|),%NumWords(@BigAddTmpInt,|)) {
  #Var BigAddTmp1 {~%If(~%i>%NumWords(@BigAddInt,|),0,~%Word(~@BigAddInt,|,~%i))};
  #Var BigAddTmp2 {~%If(~%i>%NumWords(@BigAddTmpInt,|),0,~%Word(~@BigAddTmpInt,|,~%i))};
  #Math BigAddTmp3 {~@BigAddTmp1+~@BigAddTmp2+~@BigAddCarry};
  #If {~@BigAddTmp3>999} {
   #Var BigAddCarry 1;
   #Math BigAddTmp3 {~@BigAddTmp3-1000}
  } {
   #Var BigAddCarry 0
  };
  #While {~%Length(~@BigAddTmp3)<3} {#Var BigAddTmp3 {0~@BigAddTmp3}};
  #Var BigAddResult {~@{BigAddTmp3}~@{BigAddSep}~@BigAddResult};
  #Var BigAddSep {,}
 }};
 #If {@BigAddCarry=1} {#Var BigAddResult {1@BigAddSep@BigAddResult}};
 #While {%Left(@BigAddResult,1)=0} {#Var BigAddResult {%Copy(@BigAddResult,2,%Eval(%Length(@BigAddResult)-1))}};
 #ForAll {Int|Carry|Sep|Tmp1|Tmp2|Tmp3|TmpInt} {#UnVar {BigAdd%i}}
} {}

#Trigger {You press a large * reset button on a * starship simulator.} {msplay General\Misc\Beep*10@Ext;
ExpPipe {%0} {You reset the %1 starship simulator.} {};
#sub {@ExpString}} {Main}
#Trigger {* crawls in through a large panel.} {
 #if {%Pos(stream of light,%1)=0} {
  msplay Ship\Misc\ExitDucts*2@Ext
 } {
  msplay Ship\Misc\EnterDucts*2@Ext;
  MirianiSoundPlay Footstep General\Footsteps\Duct*7@Ext
 }
} {Main|Starship}
#Trigger {* crawls in through a small panel.} {
msplay Ship\Misc\ExitDucts*2@Ext
} {Main|Starship}
#Trigger {* lifts a small panel and crawls through it.} {
  msplay Ship\Misc\EnterDucts*2@Ext
} {Main|Starship}
#Trigger {You lift a small panel and crawl through *} {
 msplay Ship\Misc\EnterDucts*2@Ext;
 ExpPipe {%0} {:Voice:%0} {};
 #Sub {@ExpString}
} {Main|Starship}
#Trigger {You flip a heavy switch, *.} {
 msplay Ship\Misc\Lever*5@Ext;
 #if {%pos(power systems,%1)>0} {
  #if {%pos(enabling,%1)>0} {
   mrplay @RoomSound
  } {
   mrplay Ship\Rooms\NoPower@Rext
  }
 }
} {Main}
#Trigger {* pushes a small lever away from *, activating the ship's systems.} {msplay Ship\Misc\Lever*5@Ext} {Main|Starship}
#Trigger {* pulls a small lever toward *, deactivating the ship's systems.} {msplay Ship\Misc\Lever*5@Ext} {Main|Starship}
#Trigger {A small console nearby flashes for your attention. A quick glance reveals that you have new mail waiting for you at*.} {msplay Ship\Misc\NewMail@Ext;
ExpPipe {%0} {A small console nearbye tells you have new mail waiting for you at %1.} {You have new mail at %1.};
#sub {@ExpString}} {Main|Starship}
#Trigger {You hear a loud clang as robotic arms seize the ship and transport it to the *.} {#if {%1 = "landing pad"} {
msplay Ship\Misc\LandingPad@Ext
};
#if {%1 = "garage"} {
msplay Ship\Misc\Garage@Ext
};
ExpPipe {%0} {Ship transported to %1.} {};
#sub {@ExpString}} {Main}
#Trigger {A loud whirring sound comes from the * as * haul cargo *.} {mrandomplay Ship\Misc\Transfer*3@Ext;
#Gagline 1 all} {Main|Starship}
#Trigger {A loud bang emanates from the * as * haul cargo *.} {mrandomplay Ship\Misc\Transfer*3@Ext;
#Gagline 1 all} {Main|Starship}
#Trigger {*A fuzzy creature, appearing momentarily dazed, disengages itself from *.*} {msplay General\Combat\Fuzzy@Ext} {Main|Starship}
#Trigger {* *s through the hatch.} {msplay Ship\Misc\Hatch@Ext} {Main}
#Alias {music} {cmusic} {}
#Trigger {A security drone suddenly appears to escort you off the ship*} {thememusicfadeout;
msplay Ship\Misc\AirlockExit@Ext} {Main|Starship}
#Trigger {Space.} {
 #Sub {The ship is in space.};
 InSpace 1
} {Main|Starship}
#Trigger {* out of the *-person * "*".} {
 #If {%Pos(~",%1)=0} {
  mrandomplay Ship\Misc\AirlockExit@Ext
 }
} {Main}
#Trigger {* through the hatch of the *-person * "*".} {
 #If {%Pos(~",%1)=0} {
  #If {%4<>@ZoneName} {
   mrandomplay Ship\Misc\AirlockEnter@Ext {%Eval(@Vol - 20)}
  } {
   msplay Ship\Misc\AirlockEnter@Ext {%Eval(@Vol - 20)}
  }
 }
} {Main}
#Trigger {You * through *.} {
 #if {%ifword(%1,@WalkStyles,%CRLF)=1} {
  #Var ShipMiscTmp1 {%Replace(%2,~",)};
  #if {%ifword(%word(@ShipMiscTmp1, ,2),unlocked broken, )>0} {
   DontStopSpeaking;
   #say {%Word(%2, ,2)};
   msplay Ship\Misc\AirlockEnter@Ext
  } {
   #if {@ShipMiscTmp1 <> "the hatch"} {
    msplay General\Misc\Door@Ext
   } {
    msplay Ship\Misc\Hatch@Ext
   }
  }
 }
} {Main}
#Trigger {You * out of the ship.} {
 thememusicfadeout;
 #Gagline 1 ALL;
 msplay Ship\Misc\AirlockExit@Ext
} {Main|Starship}
#Trigger {*A small light near the airlock of the * "*" flashes briefly.} {msplay General\Rooms\Beep18@RExt} {Main}
#Trigger {* is deposited nearby.} {mrandomplay Atmo\CargoDeposited@Ext} {Main}
#Trigger {Several drones begin depositing canisters of gas nearby.} {mrandomplay Atmo\CargoDeposited@Ext} {Main}
#Trigger {* drags you outside the ship.} {thememusicfadeout} {Main}
#Trigger {The * opens and allows you to pass.} {
 msplay {General\Misc\door open@Ext};
 ExpPipe {%0} {%0} {:Voice:%0};
 #Sub {@ExpString}
} {Main}
#Trigger {A * air freshener freshens the room with the scent of *.} {msplay Ship\Misc\Scent@Ext} {Main}
#Trigger {Power to that system has been cut.} {msplay Ship\Misc\PowerCut@Ext} {Main}
#Trigger {A mechanical groan permeates the room as *.} {
 #If {%Pos(aside,%1)>0} {
  ExpPipe {%0} {%0} {:Voice:Door opening.};
  MirianiSoundPlay Standard {Ship\Misc\DoorOpen@Ext}
 } {
  ExpPipe {%0} {%0} {:Voice:Door closing.};
  MirianiSoundPlay Standard {Ship\Misc\DoorClose@Ext}
 };
 #Sub {@ExpString}
} {Main|Starship}
#Trigger {A mechanical groan gradually subsides as the *, * the exit *.} {
 #If {%Pos(block,%2)>0} {
  ExpPipe {%0} {%0} {%3 closed.}
 } {
  ExpPipe {%0} {%0} {%3 open.}
 };
 MirianiSoundPlay Standard {Ship\Misc\DoorSettled@Ext};
 #Sub {@ExpString}
} {Main|Starship}
#Trigger {Your communicator rapidly beeps three times as it's paired to the ship.} {msplay Ship\Comm\RelayJoin@Ext} {Main|Starship}
#Trigger {It's already *.} {
 ExpPipe {%0} {%0} {Already %1.};
 #Sub @ExpString
} {Main}
#Trigger {Those coordinates are occupied by this ship.} {
 msplay General\Misc\Cancel@Ext
} {Main|Starship}
#Trigger {The *ship is already moving.} {
 msplay General\Misc\Cancel@Ext
} {Main|Starship}
#Trigger {The *ship must be * mode.} {
 msplay General\Misc\Cancel@Ext
} {Main|Starship}
#Trigger {The ship is already on *.} {
 #var auto 1;
 msplay General\Misc\Cancel@Ext
} {Main|Starship}
#Trigger {[[]##/##/#### - *:* [ap].m.] (*) *} {
 ExpPipe {%0} {%0} {%8  [%4:%5 %6.m. - %1/%2/%3]};
 #Sub {@ExpString}
} {Main}
#Trigger {* instructs the computer to begin scanning for debris.} {
 msplay {General\Misc\Beep*10.wav};
 ExpPipe {%0} {:Voice:%0} {:Voice:%1 scans for debris.};
 #sub {@ExpString}
} {Main|Starship}
#Trigger {There is insufficient sensor bandwidth for additional debris scanning.} {
 ExpPipe {%0} {:Voice:%0} {};
 #sub {@ExpString}
} {Main|Starship}
#Trigger {The *-* * "*" is located at *.} {
 ExpPipe {%0} {%1-%2 %3 %4 at %5} {%4 at %5 (%3)};
 #Sub @ExpString
} {Main}
#Trigger {The * is suddenly seized by several robotic arms and hauled into the starship garage.} {mrandomplay Ship\Misc\Garage@Ext} {Main}
#Trigger {* pull* a series of straps around * and securely buckle* into*} {msplay General\Misc\Buckle*3@Ext} {Main|Starship}
#Trigger {* release* the buckle of * seatbelt, causing it to retract back into *} {msplay General\Misc\Unbuckle*3@Ext} {Main|Starship}
#Trigger {The shot is absorbed by a massive loudspeaker on *, which seems to become gloriously silent.} {mwplay Ship\Combat\ExtDisable*2@Ext} {Main}
#Trigger {A loud burst of static suddenly comes from the loudspeaker on *. You assume it has been reactivated.} {mwplay Ship\Combat\ExtEnable*4@Ext} {Main}
#Trigger {You suddenly notice the absence of * as the smell of * wafts into the *.} {msplay Ship\Misc\Scent@Ext} {Main}
#Trigger {You hear a satisfying click as the ship's docking ring is *.} {msplay Ship\Move\RingEnd@Ext} {Main}
#Trigger {You hear a mechanical rumbling sound as the ship*s repair arm goes to work.} {
 ExpPipe {%0} {Repair arm activated.} {};
 msplay Ship\Misc\RepairArm@Ext;
 #Sub {@ExpString}
} {Main}
#Trigger {You watch as a* is brought in by several salvage lines.} {msplay Ship\Misc\PanelsOff@Ext} {Main}
#Trigger {You follow * outside the ship*} {
 #var Following 1;
 thememusicfadeout;
 msplay Ship\Misc\AirlockExit@Ext
} {Main}
#Trigger {You float out of the ship into space.*} {msplay Ship\Misc\AirlockExit@Ext} {Main}
#Trigger {You hear somebody knocking on the blast door to the *.} {msplay General\Misc\DoorChime@Ext} {Main}
#Trigger {You snatch * from the cold, cruel hands of space.} {msplay General\Misc\SnatchArtifact*2@Ext} {Main}
#Trigger {You press a button and quickly retrieve a printout of the ship's*.} {msplay Ship\Computer\Print@Ext} {Main|Starship}
#Trigger {You abort the *.} {
 ExpPipe {%0} {%0} {%1 aborted.};
 msplay Ship\Computer\Abort@Ext;
 #Sub {@ExpString}
} {Main}
#Trigger {You hear a thunk as a salvaging line is deployed into space. You watch as the line slowly winds its way to a piece of debris, projects an energy net around it and begins to slowly make its way back to the ship.} {
 ExpPipe {%0} {Salvage line deployed.} {};
 msplay Ship\Misc\Salvage*3@Ext;
 #Sub {@ExpString}
} {Main}
#Trigger {You are turned away by the locked hatch of *.} {msplay General\Misc\CantGo*5@Ext} {Main}
