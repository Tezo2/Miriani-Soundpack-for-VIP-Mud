#Trigger {A* alliance * beeps, notifying you *.} {msplay General\Device\AllianceComputer@Ext} {Main}
#Trigger {A* beeps quietly, indicating to you that there is a n*. It was p*} {
 #Var DevicesTmp1 {%Trim(%Word(%Delete(%2,1,14),/,1))};
 #Var DevicesTmp2 {%Trim(%Word(%Delete(%2,1,14),/,2))};
 #If {@DevicesTmp1="General"} {
  #If {@DevicesTmp2="Name Changes"} {
   msplay {General\Device\New Post1@Ext}
  } {
   msplay {General\Device\New Post2@Ext}
  }
 } {
  #If {@DevicesTmp1="OOC"} {
   msplay {General\Device\New Post3@Ext}
  } {
   msplay {General\Device\New Post4@Ext}
  }
 };
 #Var DevicesTmp1 {%Pos(~",%3)};
 #Var DevicesTmp2 {%Copy(%3,1,%Eval(@DevicesTmp1-2)): %Copy(%3,%Eval(@DevicesTmp1+1),%Eval(%Length(%3)-@DevicesTmp1-2))};
 ExpPipe {%0} {N%2, p@DevicesTmp2} {N%2, p@DevicesTmp2};
 #Sub {@ExpString};
 PushMessage Posts {[%Delete(%2,1,14)] P@DevicesTmp2}
} {Main}
#Trigger {* take* a snapshot of *.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Device\Snapshot*5@Ext
 }
} {Main}
#Trigger {You tune a* metafrequency * to *.*.} {
 msplay General\Comm\Tune@Ext
} {Main}
#Trigger {Your communicator reports that the target is unavailable. They might not be awake to hear your transmission or they may have private communications disabled.} {
 ExpPipe {%0} {The target is not available.} {Not available};
 #Sub {@ExpString}
} {Main}
#Trigger {You*activate *.} {
 #If {%NumWords(%0,~")=1} {
  #If {%2="a blast door control"} {
   #GagLine 1 All
  } {
   #Var DevicesTmp1 {%Trim(%1)};
   #If {@DevicesTmp1=""} {
    msplay General\Device\Activate@Ext
   } {
    #If {@DevicesTmp1="de"} {
     msplay General\Device\Deactivate@Ext
    }
   }
  }
 }
} {Main}
#Trigger {You turn *.*} {
 #Var DevicesTmp1 {%Word(%1, ,%NumWords(%1, ))};
 #Var DevicesTmp1 {%Replace(@DevicesTmp1,~",)};
 #Var DevicesTmp1 {%Replace(@DevicesTmp1,~,,)};
 #Var DevicesTmp2 {%Replace(%2,~",)};
 #If {%IsNumber(@DevicesTmp1)=1} {
  #If {@DevicesTmp2="00 credits."} {
   msplay {General\Misc\Credits*4@Ext}
  }
 } {
  #If {@DevicesTmp1="on"} {
   msplay {General\Device\activate@Ext}
  } {
   #If {@DevicesTmp1="off"} {
    msplay {General\Device\deactivate@Ext}
   }
  }
 }
} {Main}
#TRIGGER {* presses a small button on a compact black video camera, deactivating it.} {msplay General\Device\CameraOff@Ext} {Main}
#Trigger {* indicates that the scan *.} {
 #If {%NumWords(%0,~")=1} {
  #Var DevicesTmp1 {%Word(%2, ,%NumWords(%2, ))};
  #If {@DevicesTmp1="successful"} {
   ExpPipe {%0} {Scan successful} {};
   #Sub {@ExpString};
   msplay General\Device\SurveyComplete@Ext
  } {
   #If {@DevicesTmp1="failed"} {
    ExpPipe {%0} {Scan failed.} {};
    #Sub {@ExpString};
    msplay General\Device\ScanFailed@Ext
   }
  }
 }
} {Main}
#Trigger {That doesn't seem to contain any useful information out here.} {
 #If {@ShowPointsOOLS=1} {
  #Sub {You have %Replace(@LicensePoints,|,.) LP, and %Replace(@CombatPoints,|,.) CP stored in memory.}
 }
} {Main}
#Trigger {Destination *Sector *Coordinates*} {
 #Var DevicesTmp1 {%Replace(%Trim(%1%2%3),~",)};
 #If {@DevicesTmp1=""} {
  #Say {Results:} NoDisplay;
  ExpPipe {%0} {%0} {:Voice:%0};
  #Sub {@ExpString}
 }
} {Main}
#Trigger {You access * and note you have *.* license points and *.* combat points.} {
 #If {%NumWords(@LicensePoints,|)<>2} {
  #If {%IsNumber(@LicensePoints)=1} {#Var LicensePoints {@LicensePoints|0}} {#Var LicensePoints {%2|%3}}
 };
 #If {%NumWords(@CombatPoints,|)<>2} {
  #If {%IsNumber(@CombatPoints)=1} {#Var CombatPoints {@CombatPoints|0}} {#Var CombatPoints {%4|%5}}
 };
 #Math DevicesTmp2 {%2-%Word(@LicensePoints,|,1)};
 #Math DevicesTmp3 {%3-%Word(@LicensePoints,|,2)};
 #If {@DevicesTmp3<0} {
  #Math DevicesTmp3 {@DevicesTmp3+100};
  #If {@DevicesTmp2>0} {#Math DevicesTmp2 {@DevicesTmp2-1}} {
   #If {@DevicesTmp2=0} {#Var DevicesTmp2 {-@DevicesTmp2}};
   #Math DevicesTmp3 {100-@DevicesTmp3}
  }
 } {
  #If {@DevicesTmp2<0} {
   #If {@DevicesTmp3>0} {
    #Math DevicesTmp2 {@DevicesTmp2+1};
    #Math DevicesTmp3 {100-@DevicesTmp3}
   };
   #If {@DevicesTmp2=0} {#Var DevicesTmp2 {-0}}
  }
 };
 #If {%Eval(@DevicesTmp3\10)=0} {#Math DevicesTmp3 {@DevicesTmp3/10}} {
  #If {@DevicesTmp3<10} {#Var DevicesTmp3 {0@DevicesTmp3}}
 };
 #Var DevicesTmp1 {@DevicesTmp2.@DevicesTmp3};
 #Math DevicesTmp2 {%4-%Word(@CombatPoints,|,1)};
 #Math DevicesTmp3 {%5-%Word(@CombatPoints,|,2)};
 #If {@DevicesTmp3<0} {
  #Math DevicesTmp3 {@DevicesTmp3+100};
  #If {@DevicesTmp2>0} {#Math DevicesTmp2 {@DevicesTmp2-1}} {
   #If {@DevicesTmp2=0} {#Var DevicesTmp2 {-@DevicesTmp2}};
   #Math DevicesTmp3 {100-@DevicesTmp3}
  }
 } {
  #If {@DevicesTmp2<0} {
   #If {@DevicesTmp3>0} {
    #Math DevicesTmp2 {@DevicesTmp2+1};
    #Math DevicesTmp3 {100-@DevicesTmp3}
   };
   #If {@DevicesTmp2=0} {#Var DevicesTmp2 {-0}}
  }
 };
 #If {%Eval(@DevicesTmp3\10)=0} {#Math DevicesTmp3 {@DevicesTmp3/10}} {
  #If {@DevicesTmp3<10} {#Var DevicesTmp3 {0@DevicesTmp3}}
 };
 #Var DevicesTmp2 {@DevicesTmp2.@DevicesTmp3};
 #Var DevicesTmp3 {%1};
 #Var DevicesTmp4 {%2.%3};
 #Var DevicesTmp5 {%4.%5};
 #Var DevicesTmp6 {%Replace(@PointUnitOutputString,$UNIT,~@DevicesTmp3)};
 #Var DevicesTmp6 {%Replace(@DevicesTmp6,$LP,~@DevicesTmp4)};
 #Var DevicesTmp6 {%Replace(@DevicesTmp6,$CP,~@DevicesTmp5)};
 #Var DevicesTmp6 {%Replace(@DevicesTmp6,$DIFFLP,~@DevicesTmp1)};
 #Var DevicesTmp6 {%Replace(@DevicesTmp6,$DIFFCP,~@DevicesTmp2)};
 #Var DevicesTmp6 {%Replace(@DevicesTmp6,$CRLF,%CRLF)};
 #Sub {%Expand(@DevicesTmp6)};
 #UnVar DevicesTmp2;
 #UnVar DevicesTmp3;
 #UnVar DevicesTmp4;
 #UnVar DevicesTmp5;
 #UnVar DevicesTmp6;
 SetPreservedVariable LicensePoints {%2|%3};
 SetPreservedVariable CombatPoints {%4|%5}
} {Main}
#Trigger {* flick* a switch *.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  #If {%IfWord(%1,You,|)=1} {
   msplay General\Device\Switch@Ext
  } {
   msplay General\Device\Switch@Ext -20
  }
 }
} {Main}
#Trigger {You carefully replace the film in * camera.} {msplay General\Device\LoadFilm@Ext} {Main}
#Trigger {You press a small button on * and watch as * fades into nothing.} {msplay General\Device\ForceFieldSuitOff@Ext} {Main}
#Trigger {You press a small button on * and watch as * around you and slowly fades into a barely noticeable haze.} {msplay General\Device\ForceFieldSuitOn@Ext} {Main}
#Trigger {You press a large * button.} {msplay General\Misc\Beep*10@Ext} {Main}
#Trigger {You activate a* handheld planetary surveyor and begin scanning the area.} {
 ExpPipe {%0} {:Voice:%0} {};
 #Sub {@ExpString}
} {Main|Ground}

#Var Tracked 0
#Trigger {* makes a selection on a* Lore computer.} {
 #If {%Pos([,%1)=0 and %Pos(~(,%1)=0} {
  ExpPipe {%0} {%1 uses a%2 Lore.} {%1 uses a%2 Lore.};
  #Sub {@ExpString}
 }
} {Main}
#Trigger {You make a selection on a* Lore computer.} {
 ExpPipe {%0} {:Voice:A%1 Lore.} {};
 #Sub {@ExpString}
} {Main}
#Trigger {Your * Lore computer suddenly beeps quietly, indicating a new incoming *.} {
 msplay General\Lore\Import@Ext;
 ExpPipe {%0} {New incoming %2 in your %1 Lore.} {New %2 in your %1 Lore.};
 #Sub {@ExpString}
} {Main}
#Trigger {A* Lore computer beeps quietly, indicating that there * to import.} {
 msplay General\Lore\FilesWaiting@Ext;
 ExpPipe {%0} {New %Word(%2, ,%NumWords(%2, )) to import in a%1 Lore.} {New %Word(%2, ,%NumWords(%2, )) to import in a%1 Lore.};
 #Sub {@ExpString}
} {Main}
#Trigger {Several short bleeps emit from a* Lore computer, followed by a* "*"*.} {
 msplay General\Lore\Print@Ext;
 ExpPipe {%0} {A%1 Lore prints a %2 %3.} {:Voice:A%1 Lore prints a %2 %3.};
 #Sub {@ExpString}
} {Main}
#Trigger {Enter a name for the new file:} {
 msplay General\Lore\Name@Ext;
 ExpPipe {%0} {:Voice:%0} {:Voice:Filename:};
 #Sub {@ExpString}
} {Main}
#Trigger {File "*" exported.} {
 msplay General\Lore\Export@Ext;
 ExpPipe {%0} {:Voice:%0} {:Voice:%1 exported.};
 #Sub {@ExpString}
} {Main}
#Trigger {Via the TransLink network, the LoreTech device "*" reports current location pinpointed *} {
 msplay General\Lore\Track@Ext;
 #If {%Word(%2, ,1)="aboard" and %NumWords(%2,~")=3} {
  #If {%Word(%2,~",2)=%1} {
   ExpPipe {%0} {LoreTech device "%1" %2} {T%Delete(%2,1,8)}
  } {
   ExpPipe {%0} {LoreTech device "%1" %2} {%1 %2}
  }
 } {
  ExpPipe {%0} {LoreTech device "%1" %2} {%1 %2}
 };
 #Sub {@ExpString}
} {Main}
#Trigger {The LoreTech device "*" could not be located.} {
 ExpPipe %0 {LoreTech device "%1" could not be located} {%1 could not be located};
 #Sub @ExpString
} {Main}
#Trigger {No LoreTech devices located on TransLink network with the ID tag of "*."} {
 ExpPipe %0 {LoreTech device "%1" could not be found} {%1 could not be found};
 #Sub @ExpString
} {Main}
#Trigger {File "*" successfully transmitted to "*."} {
 msplay General\Lore\Transmit@Ext;
 ExpPipe {%0} {1 successfully transmitted to %2.} {%1 transmitted to %2.};
 #Sub {@ExpString}
} {Main}
#Trigger {LoreTech Personal Lore Computer - [[]*]} {
 #If {%IfWord(%1,Root Folder,|)=1 or %Pos(File:,%1)>0 or %Pos(Folder:,%1)>0} {
  ExpPipe {%0} {Lore Memo %1} {%1}
 } {
  ExpPipe {%0} {:Voice:Lore %1} {};
  #If {%Pos(Lore Lore ,@ExpString)>0} {#Var ExpString {%Copy(@ExpString,6,%Length(@ExpString)-5)}}
 };
 #Sub {@ExpString}
} {Main}
#Trigger {The display blurs suddenly as the current time and date, * * * *:*:* * GST, fades in, shimmers briefly and fades out, letting the display return to normal.} {
 ExpPipe {%0} {%0} {%4:%5:%6, %1 %2 %3, %7};
 #Sub {@ExpString}
} {Main}
#Trigger {*. A barely visible grid of red and green lights runs along * for the briefest of moments*} {msplay General\Lore\SuccessfulImage@Ext} {Main}
#Trigger {The result of your equation, *, briefly appears on the display.} {
 ExpPipe {%0} {The result of your equation, %1.} {%1};
 #Sub @ExpString
} {Main}
#Trigger {Item: *} {
 #If {@ModifyShoppingGuide=1} {
  #GagLine 1 All;
  #Var LoreTmpItem %1;
  #Class Shopping Enable
 }
} {Main}
#Trigger {Price: *} {
 #Var LoreTmpPrice %1;
 #GagLine 1 All
} {Main|Shopping}
#Trigger {Points: *} {
 #Var LoreTmpPoints %1;
 #GagLine 1 All
} {Main|Shopping}
#Trigger {Combat points: *} {
 #Var LoreTmpCombatPoints %1;
 #GagLine 1 All
} {Main|Shopping}
#Trigger {Location*: *} {
 #Var LoreTmpLocation %2;
 #GagLine 1 All
} {Main|Shopping}
#Trigger {Store*: *} {
 #Var LoreTmpStore %2;
 #Var LoreTmp1 {%Replace(@ShoppingGuideFormatString,$ITEM,@LoreTmpItem)};
 #Var LoreTmp1 {%Replace(@LoreTmp1,$PRICE,@LoreTmpPrice)};
 #Var LoreTmp1 {%Replace(@LoreTmp1,$POINTS,@LoreTmpPoints)};
 #Var LoreTmp1 {%Replace(@LoreTmp1,$COMBATPOINTS,@LoreTmpCombatPoints)};
 #Var LoreTmp1 {%Replace(@LoreTmp1,$LOCATION,@LoreTmpLocation)};
 #Var LoreTmp1 {%Replace(@LoreTmp1,$STORE,@LoreTmpStore)};
 #Sub {@LoreTmp1};
 #Class Shopping Disable
} {Main|Shopping}
#Trigger {A* beeps quietly, indicating that its TransLink tracking functionality has been activated*.} {
 #Math Tracked {@Tracked+1};
 msplay General\Lore\Tracked@Ext;
 ExpPipe {%0} {%1 TransLink tracking functionality has been activated%2.} {:Voice:A%1 has been tracked%2.};
 #Sub {@ExpString}
} {Main}
#Alias {tracked} {
 #If {%1="Clear"} {
  #variable tracked 0;
  ConfPrint {Track count cleared.}
 } {
  ConfPrint {Track count: @tracked}
 }
} {}

#Class MessageBoard Disable

#Trigger {Message Board Network - *} {
 MainTrigSwitch -10;
 #If {@InterruptOnBoards=1} {speech stop};
 #Var Board %1;
 #If {@FormatMessageBoards=1} {
  #Sub {%1}
 }
} {Main}
#Trigger {Subject: *} {
 #If {@Main<>-10} {#Abort};
 #If {@FormatMessageBoards=1} {
  MainTrigSwitch 0 MessageBoard;
  #Var Subject %1;
  #GagLine 1 All
 }
} {MainOff}
#Trigger {Sender: *} {
 #Var Sender %1;
 #GagLine 1 All
} {MessageBoard}
#Trigger {Date: *} {
 #Var Date %1;
 #GagLine 1 All
} {MessageBoard}
#Trigger {Reference: *} {
 #If {%Pos(|,%1)>0} {
  #Var Reference {%Trim(%Word(%1,|,1))};
  #If {%Pos(Votes: 0  /  0,%1)>0} {
   #Var Votes {0}
  } {
   #Var Votes {%Trim(%Word(%1,:,2))}
  }
 } {
  #Var Reference {%1};
  #Var Votes {0}
 };
 #Var MsgBoardTmp1 {%Replace(@MessageBoardFormatString,.,.%CRLF)};
 #Var MsgBoardTmp1 {%Replace(@MsgBoardTmp1,$,~@)};
 #Sub {%Expand(@MsgBoardTmp1)};
 #UnVar Board;
 #UnVar Subject;
 #UnVar Sender;
 #UnVar Date;
 #UnVar Reference;
 #UnVar Votes;
 #UnVar MsgBoardTmp1
} {MessageBoard}
