#Var ScanOutputLabels {Atmospheric Composition|Available charge|Average Component Damage|Cargo|Classification|Composition|Coordinates|Courier ship status|Damage|Distance|Hull Damage|Identifiable Power Sources|IFF|Landing beacons|Launched by|Natural Resources|Occupancy|Orbiting|Pilot status|Power|Power sources|Size|Starships|Structural Integrity|Surface Conditions|Type|Weapons}
#Var ScanFormatVarLabels {@ScanOutputLabels|Alliance|Full Name|Name|New|Number of Occupants|Object Type}
#Var ScanFormatVarLabels {%Sort(@ScanFormatVarLabels,0)}
#Var ScanFormatVars {%Replace(@ScanFormatVarLabels, ,)}

; sc is an alias that exists in VIP Mud's start.set. It needs to go.
#GUnAlias sc

#Alias sc {shipscan sc {%0}} {Starship}
#Alias scan {shipscan scan {%0}} {Starship}
#Alias shipscan {
 #Var ScanTarget {%Trim(%2)};
 #Var ScanFilter {%Trim(%3)};
 #If {%Length(@ScanFilter)>0} {
  #Var ScanFilterVar {%Replace(@ScanFilter, ,)}
 } {
  #UnVar ScanFilter;
  #UnVar ScanFilterVar
 };
 #ForAll {@ScanFormatVars} {
  #Var {ScanObject%i} {};
  #Var {ScanObject%{i}Raw} {}
 };
 #Class Scanning Enable;
 #Var ScanOutputLabelsDetected 0;
 #Exec {>%1%RTrim( @ScanTarget)}
} {}

#Trigger {* (*)} {
 #If {%IfWord(%2,AIE|Blue|CHRISTMAS|Commonwealth|Courier|CTN|Green|Hale|High Guard|Krenelia|Observer|Ontanka|Pink|Praelor|Red|Unknown,|)=1} {
  #If {@ScanFilters>0 or %Defined(ScanFilter)=1} {
   #If {@DisplayOriginalScanOutput=1} {#GagLine 1 Voice} {#GagLine 1 All}
  } {
   #If {@InterruptOnScanningStarships=1} {Speech Stop}
  };
  #Var ScanObjectAllianceRaw {%2};
  #Var ScanObjectNameRaw {%1};
  #Var ScanObjectFullNameRaw {%1};
  #If {%IfWord(%2,Krenelia|Ontanka|Praelor,|)=1} {
   #If {%2="Krenelia"} {msplay Ship\Computer\PraelorIsFriend@Ext};
   #If {%2="Ontanka" and @MutePraelorIsEnemySound=0} {msplay Ship\Computer\PraelorIsEnemy@Ext};
   #If {%Pos(Praelor ,%1)=1} {#Var ScanObjectNameRaw {%Copy(%1,9,%Eval(%Length(%1)-8))}}
  } {
   #If {%IfWord(%2,AIE|Blue|CHRISTMAS|Commonwealth|Courier|CTN|Green|Hale|High Guard|Observer|Pink|Red,|)=1 and %NumWords(%1,~")=3} {
    #If {%Pos(The ,%1)=1 and %Pos(-person ,%1)>0} {
     #Var ScanObjectTypeRaw {%Word(%1,~",1)};
     #Var ScanObjectTypeRaw {%Copy(@ScanObjectTypeRaw,5,%Eval(%Length(@ScanObjectTypeRaw)-5))};
     #Var ScanObjectNameRaw {%Word(%1,~",2)}
    }
   }
  }
 }
} {Main|Scanning}
#Trigger {*: *} {
 #If {%IfWord(%1,@ScanOutputLabels,|)=1} {
  #Math ScanOutputLabelsDetected {@ScanOutputLabelsDetected + 1};
  #Var {ScanObject%Replace(%1, ,)Raw} {%2};
  #If {@ScanFilters>0 or %Defined(ScanFilter)=1} {
   #If {@DisplayOriginalScanOutput=1} {#GagLine 1 Voice} {#GagLine 1 All}
  }
 }
} {Main|Scanning}
#Trigger {Video Probe} {#Var ScanObjectObjectTypeRaw {Probe}} {Main|Scanning}
#Trigger {Interdictor} {#Var ScanObjectObjectTypeRaw {Interdictor}} {Main|Scanning}

#Alias ScanProcessOutput {
 #Class Scanning Disable;#UnVar ScanOutputLabelsDetected;
 #If {@ScanObjectDistanceRaw=1} {#Var ScanObjectDistanceUnits {unit}} {#Var ScanObjectDistanceUnits {units}};
 #If {%Left(@ScanObjectCoordinatesRaw,1)="("} {#Var ScanObjectCoordinatesRaw {%Copy(@ScanObjectCoordinatesRaw,2,%Eval(%Length(@ScanObjectCoordinatesRaw)-2))}};
 #Var ScanObjectCoordinatesRaw {%Replace(@ScanObjectCoordinatesRaw,~,,)};
 #If {%Length(@ScanObjectObjectTypeRaw)=0} {
  #If {%Length(@ScanObjectWeaponsRaw)>0} {#Var ScanObjectObjectTypeRaw {Starship};#Abort};
  #If {%IfWord(@ScanObjectClassificationRaw,O B A F G K M, )=1} {#Var ScanObjectObjectTypeRaw {Star};#Abort};
  #If {%Length(@ScanObjectOrbitingRaw)>0} {#Var ScanObjectObjectTypeRaw {Moon};#Abort};
  #If {%Length(@ScanObjectAtmosphericCompositionRaw)>0} {#Var ScanObjectObjectTypeRaw {Planet};#Abort};
  #If {%Length(@ScanObjectStructuralIntegrityRaw)>0} {#Var ScanObjectObjectTypeRaw {Station};#Abort};
  #If {%Length(@ScanObjectCompositionRaw)>0} {#Var ScanObjectObjectTypeRaw {Asteroid};#Abort};
  #If {%Length(@ScanObjectSizeRaw)>0} {#Var ScanObjectObjectTypeRaw {Debris};#Abort};
  #If {%Length(@ScanObjectDamageRaw)>0} {#Var ScanObjectObjectTypeRaw {Weapon};#Abort};
  #If {%Length(@ScanObjectObjectTypeRaw)=0} {#Var ScanObjectObjectTypeRaw {Unknown}}
 };
 #If {@ScanObjectObjectTypeRaw="Starship"} {
  #If {@ScanObjectPilotstatusRaw="New"} {#Var ScanObjectNewRaw {@ScanObjectNewRaw|New pilot}};
  #If {@ScanObjectCouriershipstatusRaw="New"} {#Var ScanObjectNewRaw {@ScanObjectNewRaw|New courier ship}};
  #If {%Length(@ScanObjectNewRaw)>0} {
   #Var ScanObjectNewRaw {%DelItem(@ScanObjectNewRaw,)};
   #Var ScanObjectNewRaw {%Replace(@ScanObjectNewRaw,|,~, )}
  };
  #If {%IfWord(Unknown,@ScanObjectPowerRaw,%CRLF)=1} {#Var ScanObjectPowerRaw {Charge Unknown}};
  #If {%IfWord(Indeterminate,@ScanObjectCargoRaw,%CRLF)=1} {#Var ScanObjectCargoRaw {}};
  #If {%Length(@ScanObjectOccupancyRaw)>0 and %IfWord(@ScanObjectOccupancyRaw,Empty|Filled|Invalid reading|Unfilled,|)=0} {
   #Var ScanObjectNumberOfOccupantsRaw {%NumWords(@ScanObjectOccupancyRaw,~,)};
   #If {@ScanObjectNumberOfOccupantsRaw=1 and %Pos( and ,@ScanObjectOccupancyRaw)>0} {#Var ScanObjectNumberOfOccupantsRaw 2}
  }
 } {
  #If {@ScanObjectObjectTypeRaw="Debris"} {
   #If {%Length(@ScanObjectTypeRaw)=0} {
    #Var ScanObjectTypeRaw {@ScanObjectSizeRaw debris};
    #Var ScanObjectSizeRaw {}
   }
  }
 };
 #Var ScanTmp1 {1};
 #Var ScanTmp2 {};
 #Var ScanObjectEverythingElse {};
 #ForAll {@ScanFormatVars} {
  #If {%Length(@{ScanObject%{i}Raw})>0} {
   #Var {ScanObject%i} {%Word(@ScanFormatVarLabels,|,@ScanTmp1): @{ScanObject%{i}Raw}};
   #Var ScanTmp2 {$%i};
   #If {%i<>"ObjectType" and %Pos(@ScanTmp2,@{ScanFormatString@ScanObjectObjectTypeRaw})=0} {
    #Var ScanObjectEverythingElse {@ScanObjectEverythingElse%CRLF@{ScanObject%i}.}
   }
  };
  #Math ScanTmp1 {@ScanTmp1 + 1}
 };
 #If {%Length(@ScanObjectEverythingElse)>0} {#Var ScanObjectEverythingElse {%Copy(@ScanObjectEverythingElse,3,%Eval(%Length(@ScanObjectEverythingElse)-3))}};
 #UnVar ScanTmp1;
 #UnVar ScanTmp2;
 msplay Ship\Computer\Scan*4@Ext;
 #If {@ScanObjectDistanceRaw=1} {msplay General\Misc\Beep10@Ext};
 InSpace 1
} {}
#Alias ScanFormat {
 #If {%Length(%1)=0} {#Var ScanFormatOutput {@{ScanFormatString@ScanObjectObjectTypeRaw}}} {#Var ScanFormatOutput {@{ScanFormatString%1}}};
 #Var ScanObject {$};
 #Var ScanFormatOutput {%Replace(@ScanFormatOutput,.,.%CRLF)};
 #Var ScanFormatOutput {%Replace(@ScanFormatOutput,@ScanObject,~@ScanObject)};
 #Var ScanFormatOutput {%Expand(@ScanFormatOutput)};
 #Var ScanTmp1 {%Length(@ScanFormatOutput)};
 #Var ScanFormatOutput {%Replace(@ScanFormatOutput,%CRLF.%CRLF,%CRLF)};
 #Var ScanFormatOutput {%Replace(@ScanFormatOutput,%CRLF%CRLF,%CRLF)};
 #While {@ScanTmp1>%Length(@ScanFormatOutput)} {
  #Var ScanTmp1 {%Length(@ScanFormatOutput)};
  #Var ScanFormatOutput {%Replace(@ScanFormatOutput,%CRLF.%CRLF,%CRLF)};
  #Var ScanFormatOutput {%Replace(@ScanFormatOutput,%CRLF%CRLF,%CRLF)}
 };
 #If {%Pos(.,@ScanFormatOutput)=1} {#Var ScanFormatOutput {%Copy(@ScanFormatOutput,2,%Eval(%Length(@ScanFormatOutput)-1))}};
 #If {%Pos(%CRLF,@ScanFormatOutput)=1} {#Var ScanFormatOutput {%Copy(@ScanFormatOutput,3,%Eval(%Length(@ScanFormatOutput)-2))}};
 #If {%Replace(%Right(@ScanFormatOutput,2),~",)=%CRLF} {#Var ScanFormatOutput {%Copy(@ScanFormatOutput,1,%Eval(%Length(@ScanFormatOutput)-2))}};
 #UnVar ScanTmp1;
 #UnVar ScanObject
} {}

#Trigger {There is nothing here to scan.} {
 msplay Ship\Computer\NothingToScan@Ext;
 #Class Scanning Disable;#UnVar ScanOutputLabelsDetected
} {Main|Scanning}
#Trigger {Nothing was detected at those coordinates.} {
 msplay Ship\Computer\NothingToScan@Ext;
 #Class Scanning Disable;#UnVar ScanOutputLabelsDetected
} {Main|Scanning}
#Trigger {That object was not found.} {
 msplay Ship\Computer\NothingToScan@Ext;
 #Class Scanning Disable;#UnVar ScanOutputLabelsDetected;
 #Class Focus Disable
} {Main|Starship}
#Trigger {You'll have better results scanning in space.} {
 #Class Scanning Disable;#UnVar ScanOutputLabelsDetected;
 #Class Focus Disable
} {Main|Starship}

#Alias {sh} {
 shipscan sc {%Trim(%0)} {Hull Damage}
} {}
#Alias {sca} {
 shipscan sc {%Trim(%0)} {Average Component Damage}
} {}
#Alias {scc} {
 shipscan sc {%Trim(%0)} {Coordinates}
} {}
#Alias {scd} {
 shipscan sc {%Trim(%0)} {Distance}
} {}
#Alias {sch} {
 shipscan sc {%Trim(%0)} {Hull Damage}
} {}
#Alias {sco} {
 shipscan sc {%Trim(%0)} {Occupancy}
} {}
#Alias {scp} {
 shipscan sc {%Trim(%0)} {Power}
} {}
#Alias {scs} {
 shipscan sc {%Trim(%0)} {Cargo}
} {}
#Alias {scw} {
 shipscan sc {%Trim(%0)} {Weapons}
} {}

#Alias CGCBackend {
 #If {%IsNumber(%1)=1 AND %IsNumber(%2)=1 AND %IsNumber(%3)=1 AND %IsNumber(%4)=1 AND %IsNumber(%5)=1 AND %IsNumber(%6)=1} {
  #Math CGCTmpX {%Max(%1,%4) - %Min(%1,%4)};
  #Math CGCTmpY {%Max(%2,%5) - %Min(%2,%5)};
  #Math CGCTmpZ {%Max(%3,%6) - %Min(%3,%6)};
  #Var CGCDestString {};
  #If {@CGCTmpX>0} {
   #If {@CGCTmpX=1} {#Var CGCTmpX {@CGCTmpX sector}} {#Var CGCTmpX {@CGCTmpX sectors}};
   #If {%1<%4} {#Var CGCTmpX {@CGCTmpX west}} {#Var CGCTmpX {@CGCTmpX east}};
   #Var CGCDestString {@CGCDestString|@CGCTmpX}
  };
  #If {@CGCTmpY>0} {
   #If {@CGCTmpY=1} {#Var CGCTmpY {@CGCTmpY sector}} {#Var CGCTmpY {@CGCTmpY sectors}};
   #If {%2<%5} {#Var CGCTmpY {@CGCTmpY north}} {#Var CGCTmpY {@CGCTmpY south}};
   #Var CGCDestString {@CGCDestString|@CGCTmpY}
  };
  #If {@CGCTmpZ>0} {
   #If {@CGCTmpZ=1} {#Var CGCTmpZ {@CGCTmpZ sector}} {#Var CGCTmpZ {@CGCTmpZ sectors}};
   #If {%3<%6} {#Var CGCTmpZ {@CGCTmpZ up}} {#Var CGCTmpZ {@CGCTmpZ down}};
   #Var CGCDestString {@CGCDestString|@CGCTmpZ}
  };
  #If {@CGCDestString<>""} {
   #Var CGCDestString {%Delete(@CGCDestString,1,1)};
   #Var CGCTmp {%NumWords(@CGCDestString,|)};
   #If {@CGCTmp=3} {
    #Var CGCDestString {%Word(@CGCDestString,|,1), %Word(@CGCDestString,|,2), and %Word(@CGCDestString,|,3)}
   } {
    #If {@CGCTmp=2} {
     #Var CGCDestString {%Word(@CGCDestString,|,1) and %Word(@CGCDestString,|,2)}
    }
   }
  } {
   #Var CGCDestString {right here}
  }
 } {
  #Var CGCDestString {nowhere, because you didn't type CGC X Y Z}
 }
} {}
#Var SectorBeacons {
0: -603120, 41, 450
1: -603080, 1, 420
2: -603079, 0, 420
3: -603078, -1, 420
4: -603077, -2, 420
5: -603200, 22, -375
6: -603328, 58, 467
7: -603109, 20, -117
8: -603129, -58, 200
9: -603105, 7, 50
10: -603225, -58, 271
11: -603128, 22, 184
12: -603229, 58, 200
13: -603129, 30, 580
14: -603107, 59, 479
15: -603126, 40, 507
16: -602433, 111, 57
17: -602419, 1, 21
18: -602410, -21, -1
19: -604622, 27, 167
20: -602410, -21, 90
21: -602422, -47, 117
22: -602432, -52, 217
23: -602632, 40, 467
24: -602630, -13, 214
25: -602662, 109, 417
26: -602622, 27, 167
27: -602232, 23, 377
28: -602637, 32, 400
29: -602632, 22, 407
30: -602632, 13, 467
31: -602662, -101, 251
32: -602652, -115, 211
33: -602682, -50, 219
34: -602750, -25, 250
35: -602620, 10, 222
36: -602500, 35, -95
37: -602611, 49, -324
38: -602823, 143, -67
39: -602555, 47, 324
40: -602876, 83, 199
115: -603127, 103, 466
}
#Alias cgc {
 #If {%IsNumber(%1)=1 AND %IsNumber(%Trim(%0))=1} {
  #Var CGCTarget {%Trim(%0)};
  #Var CGCTmp {%Pos(%CRLF@CGCTarget:,@SectorBeacons)};
  #If {@CGCTmp>0} {
   #Var CGCTmp {%Delete(@SectorBeacons,1,@CGCTmp)};
   #Var CGCTmp {%Word(@CGCTmp,:,2)};
   #Var CGCTmp {%Word(@CGCTmp,%CRLF,1)};
   #Var CGCX {%Word(@CGCTmp,~,,1)};
   #Var CGCY {%Word(@CGCTmp,~,,2)};
   #Var CGCZ {%Word(@CGCTmp,~,,3)};
   #Var CGCX {%Trim(@CGCX)};
   #Var CGCY {%Trim(@CGCY)};
   #Var CGCZ {%Trim(@CGCZ)};
   #Var CGCTarget {Sector @CGCTarget};
   #Class cgc enable;
   gc
  } {
   #UnVar CGCTarget;
   ConfPrint {CGC has no information about that sector number.}
  }
 } {
  #Var CGCX {%Replace(%1,~,,)};
  #Var CGCX {%Replace(@CGCX,~(,)};
  #Var CGCY {%Replace(%2,~,,)};
  #Var CGCZ {%Replace(%3,~),)};
  #If {%IsNumber(@CGCX)=1 AND %IsNumber(@CGCY)=1 AND %IsNumber(@CGCZ)=1} {
   #Var CGCTarget {%4};
   #Class cgc enable;
   gc
  } {
   #UnVar CGCTarget;
   ConfPrint {Syntax: CGC <galactic coordinates>, or CGC <sector number>}
  }
 }
} {}
#Trigger {Current galactic coordinates: *, *, *} {
 #If {%IsNumber(@CGCX)=1 and %IsNumber(@CGCY)=1 and %IsNumber(@CGCZ)=1} {
  #If {@UseSMStyle=1} {
   #Exec {CalculateRelativeNumericDir @CGCX @CGCY @CGCZ %1 %2 %3}
  } {
   #Exec {CGCBackend @CGCX @CGCY @CGCZ %1 %2 %3}
  };
  msplay Ship\Computer\Scan*4@Ext;
  #If {%Replace(@CGCTarget,~",)<>""} {
   #Var CGCTmp {@CGCTarget is}
  } {
   #Var CGCTmp {The target coordinates lie}
  };
  #sub {@CGCTmp %If(@UseSMStyle=1,@RelativeNumericDir,@CGCDestString).}
 };
 #Class cgc disable;
 #UnVar CGCTarget;
 #UnVar CGCTmp;
 #UnVar CGCTmpX;
 #UnVar CGCTmpY;
 #UnVar CGCTmpZ;
 #UnVar CGCDestString
} {Main|cgc}
#Trigger {The ship must be powered before you can do that.} {#class cgc disable} {Main|cgc}
#Trigger {Your ship has no Galactic Map Unit.} {#class cgc disable} {Main|cgc}
#Trigger {I don't understand that.} {#class cgc disable} {Main|cgc}
#Trigger {Wait *} {#class cgc disable} {Main|cgc}
#Trigger {The ship is not in space.} {#class cgc disable} {Main|cgc}
#Trigger {Invalid selection.} {#class cgc disable} {Main|cgc}
#TRIGGER {Current galactic coordinates: *, *, *} {#var GalCoordsX %1;
#var GalCoordsY %2;
#var GalCoordsZ %3} {Main}
#Trigger {Objects*Direction*Lightyears*Coordinates*} {
 ExpPipe {%0} {%0} {:Voice:%0};
 #sub {@ExpString};
 msplay Ship\Computer\LRScan*4@Ext
} {Main}
#TRIGGER {Coordinate scan for: *:} {
 ExpPipe {%0} {%1:} {};
 #sub {@ExpString}
} {Main}
#Trigger {The target coordinates lie *} {msplay Ship\Computer\Scan*4@Ext} {Main}
#Trigger {The sensors detect no objects in range of the long-range scanners.} {
 ExpPipe {%0} {No objects in range of the long-range scanners.} {Nothing detected};
 #Sub {@ExpString}
} {Main|Starship}
#Trigger {General sector report for *: *:} {
 #Class Scanning Disable;#UnVar ScanOutputLabelsDetected;
 ExpPipe {%0} {%0} {%1: %2:};
 #Sub {@ExpString}
} {Main|Scanning}
