#If {%IsNumber(%Version)=0} {
 #Say {WARNING! You seem to be running VIP Mud 1.1, and therefore you'll need to install the 1.1 compatible version of this soundpack.}
}
#If {%Defined(Connected)=0} {#Var Connected 0}
#Var GagEverything 0
#Var Idle 0
#Var LoadedSoundpackVersion {}

#PC 0 Stop
#Class LoggingIn Disable
#Class Main Enable
#Class MainOff Disable
#Var Main 1

#Var DefaultScriptFiles {ShipComputer
Asteroid
CameraFeed
Combat
Communication
Devices
History
KeyStrokes
Misc
Rooms
Scanning
ShipCombat
ShipMisc
ShipMovement
SmFilter
Socials
TriggerTables
YouActions
ConfManagerMenus}

#Var ExtraScriptFiles {
Babies^Baby sounds
Cannons^Alerts you of how many shots you have remaining, and plays a sound after the last shot is fired
CGC^The calculator routines for galactic coordinates
Development^Tools for soundpack development
Fishing^Sounds for fishing at Augustus
Games^Sounds for games such as air hockey, skeeball, assault, etc
LaserTag^Sounds for Laser Tag
Shenanigans^Sounds for slime dispensers, blades, etc
Socials^Sounds for kitchen appliances, swimming, campfires, swings, showers, punching bags, and much more
Typing^Allows you to type with only one hand, using the number pad
}

; VIP Mud config overrides:
#Config ScriptReadOnly True
#Config AutoSave False
#Config Explicit False
#Config SlowWalk True

#GAlias login {
 #If {%Defined(Main)=1 and %Defined(LoadedSoundpackVersion)=1} {
  #If {%Length(%CharInfo(Name))>0} {
   #If {%IfWord(Ready,%1,|)=1 or (%IfWord(%LastLine(1),Username:,%CRLF)=1 and %Defined(LoadTmp1)=0 and %Defined(LoadTmp2)=0)} {
    #Exec {>%CharInfo(Name)};
    #If {%Length(%CharInfo(Password))>0} {#Exec {>%CharInfo(Password)}};
    #UnVar AutoLogin;
    #GUnVar AutoLogin
   } {#Var AutoLogin 1}
  }
 } {
  #Wait 2;#Exec {>%CharInfo(Name)};#Exec {>%CharInfo(Password)}
 }
} {}

#Trigger {Welcome to*} {
 #If {%IfWord(%1,...| Miriani!,|)=1} {
  #Var Connected 0;
  #If {%1="..."} {
   #Say {Welcome to Miriani!};
   #GagLine 8 All
  };
  #Alarm 0 {
   #Load {@ScriptPath\Const.set};
   #Load {@SoundsPath\Main.set};
   SPLoad1 1
  }
 }
} {Main}
#Alias SPLoad1 {
 #Var LoadTmp1 {%1};
 #Alarm 0 {
  #If {@LoadTmp1<>1} {
   #Load {@ScriptPath\Const.set};
   #Load {@SoundsPath\Main.set};
   #If {%CharInfo(Connected)=1} {#Var Connected 1}
  } {
   #Config DisplayCommandsInOutput False;
   #Exec {>register_soundpack @SoundpackName | @Version}
  };
  #Load {%MudName\Variables.set};
  #If {%Defined(ConfOptionVars)=0} {#Load {@ScriptPath\ConfManager.set}};
  #If {@SetEntrySpeechOff=1} {#SetSpeech Entry Off};
  #Config ExpandCommandLineVars {%If(@PassThru=1,False,True)};
  #If {%Defined(SoundIsPlaying)=0} {#Load {@ScriptPath\SoundEngine.set}};
  #If {@LoadTmp1=1} {
   #If {@PlayStartupMusic=1} {
    MirianiSoundPlay {Startup Music} {Music\*63@Ext};
    #If {@SoundIsPlaying=1} {#Var StartupMusicHandle %PlayHandle}
   };
   #If {%Defined(AutoLogin)=1} {
    #Class LoggingIn Enable;
    login Ready
   } {
    #Class LoggingIn Disable;
    SPLoad2 {@LoadTmp1}
   };
   #If {@CommandEcho=1} {#Config DisplayCommandsInOutput True}
  } {SPLoad2 {@LoadTmp1}};
  #UnVar LoadTmp1
 }
} {}
#Trigger {Input your selection:} {#Class LoggingIn Disable;SPLoad2 1} {LoggingIn}
#Alias SPLoad2 {
 #Var LoadTmp2 {%1};
 #If {%Replace(@LoadedSoundpackVersion,.,)<>%Replace(@Version,.,)} {
  #Alarm 0 {
   #ForAll {%Replace(@DefaultScriptFiles,%CRLF,|)} {#Load {@ScriptPath\%i.set}};
   LoadExtras {NoUnload|Quiet};
   #Load {%MudName\Custom.set};
   Speech Set {@DefaultSpeechType} 1;
   #If {@AlertOnUpdate=1 and @RequiredSoundsVersion>@SoundsVersion} {
    #Say {Warning: You are not using the latest version of the Miriani Soundpack Sounds. Certain sounds might not work until you upgrade to the most recent sounds folder.};
    msplay {General\Misc\MOTD@Ext}
   };
   msplay Ship\Computer\Beep@Ext;
   #If {@LoadTmp2=0} {
    #Ignore Key Off;
    #Ignore Trigger Off;
    #If {@RoomSounds=1 and %Defined(InSpace)=1 and %Defined(RoomSound)=1 and %Defined(ZoneType)=1} {
     #If {(@InSpace=1 or %Pos(Pool,@RoomName)>0) or @ZoneType<>"starship"} {
      mrplay @RoomSound Frequency 44100
     } {
      mrplay @RoomSound Frequency 22050
     }
    };
    SPKeepAlive Start Quiet;
    #Say {Reloading complete. Please look in the room with your character so that the soundpack will know the current environment.}
   } {
    #If {%Defined(AutoLogin)=0} {
     #If {%Defined(BypassLoginMenuPostponed)=1} {
      #Exec {1};
      #UnVar BypassLoginMenuPostponed
     }
    } {login Ready}
   };
   #Var LoadedSoundpackVersion {@Version};
   #UnVar LoadTmp2
  }
 } {
  #If {@LoadTmp2=1} {
   #If {%Defined(AutoLogin)=0} {
    #If {%Defined(BypassLoginMenuPostponed)=1} {
     #Exec {1};
     #UnVar BypassLoginMenuPostponed
    }
   } {login Ready}
  };
  #UnVar LoadTmp2;
  MirianiSoundPlay Standard Ship\Computer\Beep@Ext
 }
} {}
#Trigger {[*][*][*] * [*][*][*]} {
 #Var MainTmp1 {%Replace(%1,~",)};
 #If {@MainTmp1="Connected"} {
  #GagLine 1 Voice;
  SPConnect 1;
  #Alarm 2 {SPKeepAlive Start Quiet};
  #Abort
 };
 #If {@MainTmp1="Disconnected"} {
  mrplay stop;
  #Var Connected 0;
  #PC 0 Stop;
  #If {@AutoSave=1 and %Defined(ConfManagerMenusLoaded)=1} {ConfSaveAll Quiet};
  #Abort
 };
 #If {@MainTmp1="Redirecting old connection to this port"} {
  #GagLine 1 Voice;
  #Say {Reconnected} NoDisplay;
  #PC 0 Stop;
  #UnAlarm -1;
  SPConnect 1;
  l;
  #Abort
 };
 #If {@MainTmp1="You have entered a starship simulator. To exit, please type END."} {msplay General\Device\MovieStart@Ext;#Abort};
 #If {@MainTmp1=*"The server will be shut down *"} {msplay General\Misc\Announcement@Ext;#Abort}
} {Main}

#Alias ping {
 #Var ShowPong {};
 #Var ShowPingPongLagReport {};
 #Config DisplayCommandsInOutput False;
 #Exec {>#$#soundpack_ping};
 #If {@CommandEcho=1} {#Config DisplayCommandsInOutput True}
} {}
#Trigger {[#]$[#]*} {
 #Config DisplayCommandsInOutput False;
 #GagLine 1 All;
 #Var oobTmp1 {%Replace(%1,~",)};
 #Var oobTmp1 {%Replace(@oobTmp1,~~,)};
 #If {%Defined(KeepAliveState)=1} {#Var KeepAliveState 1};
 #If {@oobTmp1="soundpack_pong"} {
  #If {%Defined(WhoOriginalHeader)=1} {
   WhoCheck {} {ReadingComplete};
   #UnGagLine;
   #Sub {%Copy(@WhoData,3)};
   #UnVar WhoData
  } {
   #Exec {>#$#soundpack_ping_reply ms};
   #If {%Defined(ShowPong)=1} {
    #UnGagLine;
    #Sub {Pong!};
    #UnVar ShowPong
   }
  }
 };
 #If {@oobTmp1="hjelp"} {
  #If {@KeepAliveRespond=1 or @KeepAlive>0} {
   #Exec {>#$#soundpack_ping_reply ms}
  };
  SPKeepAlive Start Quiet Replied;
  #If {@SaveOnIdle=1} {
   #Exec {#Alarm 0 {ConfSaveAll {%If(@GagSaveOnIdle=1,Quiet,)}}}
  }
 };
 #If {@oobTmp1=*"soundpack_lag *"} {
  #If {%Defined(ShowPingPongLagReport)=1} {
   #Var ShowPingPongLagReport {%Word(%1, ,2)};
   #Var ShowPingPongLagReport {%Word(@ShowPingPongLagReport,.,1)};
   #UnGagLine;
   #Sub {You have @ShowPingPongLagReport ms of local client lag.};
   #UnVar ShowPingPongLagReport
  }
 };
 #If {@oobTmp1=*"soundpack echo | *"} {
  #If {%Defined(SPPromptAwaitingUserInput)=1} {
   #If {@SPPromptAcceptBlankUserInput=1 or %Length(%1)>17} {
    #Exec {#Alarm 0 {SPPrompt Disable;@SPPromptCallback {%Copy(%1,18)} {@SPPromptCallbackLastArg}}}
   } {
    msplay General\Misc\Cancel@Ext;
    #Say {Aborted.} NoVoice;
    SPPrompt Disable
   }
  } {
   msplay General\Misc\Cancel@Ext
  }
 };
 #If {@CommandEcho=1} {#Config DisplayCommandsInOutput True}
} {}
#Alias SPKeepAlive {
 #If {%IsNumber(%1)=1} {
  #If {%1>0} {
   #Var KeepAlive %1;
   #If {%2<>"Quiet"} {#Say {SPKeepAlive is now set to %1 seconds.}};
   SPKeepAlive Start Quiet
  } {
   #Var KeepAlive 0;
   #If {%Defined(KeepAliveState)=1} {#UnVar KeepAliveState};
   #If {%Defined(KeepAliveHandle)=1} {#UnAlarm KeepAliveHandle;#UnVar KeepAliveHandle};
   #If {%2<>"Quiet"} {#Say {SPKeepAlive is now off.}}
  }
 } {
  #If {%1="Start"} {
   #If {@KeepAlive>0} {
    #If {%2<>"Quiet"} {
     #If {%Defined(KeepAliveHandle)=1} {
      #Say {SPKeepAlive restarted.}
     } {
      #Say {SPKeepAlive started.}
     }
    };
    #If {@KeepAlive<10} {
     #Say {Automatically adjusting your SPKeepAlive to 10 seconds, because @KeepAlive is just too short.};
     #Var KeepAlive 10
    };
    #If {%3<>"Replied"} {
     #Var KeepAliveState 0;
     #Config DisplayCommandsInOutput False;
     #Exec {>#$#soundpack_ping};
     #If {@CommandEcho=1} {#Config DisplayCommandsInOutput True}
    };
    #If {%Defined(KeepAliveHandle)=1} {#UnAlarm KeepAliveHandle};
    #Alarm KeepAliveHandle {@KeepAlive} {
     #UnVar KeepAliveHandle;
     #If {@KeepAliveState=0} {
      #Var KeepAliveState 3;
      #Say {WARNING: The soundpack reports that your connection to the server might be broken!};
      #Say {You can change the soundpack's keep alive settings by typing SPKeepAlive.};
      msplay {LaserTag/NoTargets@Ext}
     } {
      SPKeepAlive Start Quiet
     }
    }
   } {
    #If {%2<>"Quiet"} {
     #Say {First you need to give SPKeepAlive an interval to work with. Please type SPKeepAlive for more information.}
    }
   }
  } {
   #If {@KeepAlive>0} {
    #Say {SPKeepAlive is currently set to @KeepAlive seconds.}
   } {
    #Say {SPKeepAlive is currently off.}
   };
   #Say {You can set the interval of SPKeepAlive by giving it a new amount of seconds, e.g.: SPKeepAlive 90. That would set the interval to one and a half minute.};
   #Say {To turn SPKeepAlive off, set it to 0 seconds.}
  }
 }
} {}

#Alias {aboutscripts} {speech stop;
ConfPrint {@{SoundpackName}, version @{version}.
Originally written by Cory Martin.
Rewritten, updated, and maintained by @SoundpackCreators.
  Credits:
@SoundpackContributors
  Contact information:
Please use the OOC / Soundpacks board in Miriani for questions and stuffs.
}} {}
#key shift-f1 {aboutscripts} {}

#Alias MainTrigSwitch {
 #If {%Length(%2)>0} {
  #If {%Defined(ActiveClasses)=0} {#Var ActiveClasses {%2}} {#AddItem ActiveClasses {%2} NoDuplicates}
 };
 #If {%1>0} {
  #If {%Defined(ActiveClasses)=1} {
   #ForAll {@ActiveClasses} {
    #Class {%i} Disable;
    #Exec {#UnVar {%i}}
   };
   #UnVar ActiveClasses
  };
  #Var Main %1;
  #Class Main Enable;
  #Class MainOff Disable
 } {
  #If {%Length(%2)>0} {
   #ForAll {%2} {
    #Class {%i} Enable;
    #Var {%i} {%1}
   }
  };
  #If {%1<0} {#Class Main Disable;#Class MainOff Enable;#Var Main %1}
 }
}
#Alias GetWildcardValues {
 #Var WildcardTmp1 0;
 #Math WildcardTmp2 {%NumWords(%1,*)-1};
 #Var WildcardTmp3 {%2};
 #While {@WildcardTmp1<@WildcardTmp2} {
  #Math WildcardTmp1 {@WildcardTmp1+1};
  #Var WildcardTmp4 {%Word(%1,*,@WildcardTmp1)};
  #Var WildcardTmp5 {%Word(%1,*,@WildcardTmp1+1)};
  #Var {Wildcard@WildcardTmp1} {%Copy(@WildcardTmp3,%Length(@WildcardTmp4)+1)};
  #If {%Length(@WildcardTmp5)>0} {
   #Math WildcardTmp6 {%Pos(@WildcardTmp5,@{Wildcard@WildcardTmp1})-1};
   #If {@WildcardTmp6<0} {
    #Var WildcardTmp6 0;
    #Var {Wildcard@WildcardTmp1} {}
   } {
    #Var {Wildcard@WildcardTmp1} {%Copy(@{Wildcard@WildcardTmp1},1,@WildcardTmp6)}
   }
  } {
   #Var WildcardTmp6 {%Length(@{Wildcard@WildcardTmp1})}
  };
  #Var WildcardTmp3 {%Copy(@WildcardTmp3,%Length(@WildcardTmp4)+@WildcardTmp6+1)}
 }
} {}

#Alias ExpPipe {
 #Var ExpString {%{@ExperienceLevel}};
 #If {%Pos(:,@ExpString)=1} {
  #If {%NumWords(@ExpString,:)>2} {
   #Var ExpGag {%Word(@ExpString,:,2)};
   #Var ExpString {%Copy(@ExpString,%Length(@ExpGag)+3)};
   #If {%Length(@ExpString)=0} {#Var ExpString { }}
  } {#Var ExpGag {%Copy(@ExpString,2)};#Var ExpString { }};
  #If {%NumWords(@ExpGag, )=2} {#GagLine {%Word(@ExpGag, ,1)} {%Word(@ExpGag, ,2)}} {
   #If {%IsNumber(@ExpGag)=1} {#GagLine {@ExpGag} All} {#GagLine 1 {@ExpGag}}
  }
 } {
  #If {%Length(@ExpString)=0} {#Var ExpString { };#GagLine 1 All}
 }
} {}
#Alias DisplayPrint {
 #Var DisplayString {%2};
 #Var {Display%{1}IsHidden} {0};
 #If {@{HideDisplay%1}=1} {
  #Var DisplayFilterTmp1 {%Replace(%3,~",~%0x22~%)};
  #Var DisplayFilterTmp1 {%Replace(@DisplayFilterTmp1,~~,~%0x7e~%)};
  #Var DisplayFilterTmp2 {%Replace(@{Display%{1}Filter},~",~%0x22~%)};
  #Var DisplayFilterTmp2 {%Replace(@DisplayFilterTmp2,~~,~%0x7e~%)};
  #If {@DisplayFilterTmp1=*@DisplayFilterTmp2} {
   #GagLine 1 {@{Display%{1}GagType}};
   #Var {Display%{1}IsHidden} {1}
  }
 }
} {}

#Alias wn {#edit @ChangelogFilePath} {}
#Alias GagEverything {
 #Var GagEverything {%Eval(NOT @GagEverything)};
 #If {@GagEverything=1} {
  #SetSpeech Incoming Off;
  #Say {Gagging enabled. Type GagEverything, or press control f12 to disable.} VoiceOnly
 } {
  #SetSpeech Incoming On;
  #Say {Gagging Disabled.}
 }
} {}
#Alias SPConnect {
 #Var Connected 1;
 #If {%1="1"} {
  #If {%Defined(StartupMusicHandle)=1} {
   #PC @StartupMusicHandle Stop;
   #UnVar StartupMusicHandle
  };
  SetRoomSoundVolume
 };
 soundpack register
} {}
#Alias SPCommand {
 #Exec {>#$#soundpack_status commands:%Trim(%0)};
 #Exec {%Trim(%0)}
} {}
#Alias SPInfo {
 #If {%IsNumber(%Version)=0} {
  #Say {Warning: You have installed the scripts for VIP Mud 2.0, but you are using VIP Mud 1.1. It is likely that errors will occur unless you install the scripts for VIP Mud 1.1, or upgrade to VIP Mud 2.0.}
 };
 #If {@RequiredSoundsVersion>@SoundsVersion} {
  #Say {Warning: You are not using the most recent Miriani Soundpack Sounds. Certain sounds might not work until you upgrade to version @RequiredSoundsVersion of the sounds folder.}
 };
 #Say {Soundpack version: @Version};
 #If {%IsNumber(%Version)=0} {
  #Say {VIP Mud version: 1.1}
 } {
  #If {%Length(%Version)=6} {
   #Var VipTmp1 {%Left(%Version,1)};
   #If {%Copy(%Version,2,1)<>"0"} {#Var VipTmp1 {@VipTmp1%Copy(%Version,2,1)}};
   #Var VipTmp1 {@VipTmp1.%Copy(%Version,3,1)};
   #If {%Copy(%Version,4,1)<>"0"} {#Var VipTmp1 {@VipTmp1%Copy(%Version,4,1)}};
   #Var VipTmp1 {@VipTmp1.%Copy(%Version,5,1)};
   #If {%Copy(%Version,6,1)<>"0"} {#Var VipTmp1 {@VipTmp1%Copy(%Version,6,1)}};
   #Say {VIP Mud version: @VipTmp1};
   #UnVar VipTmp1
  } {
   #Say {VIP Mud version: %Version}
  }
 };
 #Say {Sounds version: @SoundsVersion};
 #Say {Scripts path: @ScriptPath};
 #Say {Sounds path: @SoundsPath};
 #Say {Themes path: @ThemesPath}
} {}
#Alias SPTest {
 #Var TestTmp1 {Soundpack Test:};
 #If {%Defined(Version)=1} {
  #Var TestTmp1 {@TestTmp1%{CRLF}+ Soundpack version seems to be @Version.}
 } {
  #Var TestTmp1 {@TestTmp1%{CRLF}- Soundpack version is missing. Try type SPRELOAD to fix this.}
 };
 #If {%Defined(DecimalChar)=1 and %Defined(LoadedSoundpackVersion)=1 and %Defined(Connected)=1 and %Defined(GagEverything)=1 and %Defined(DefaultScriptFiles)=1} {
  #Var TestTmp1 {@TestTmp1%{CRLF}+ Main.set variables seems to be loaded.};
  #If {%Replace(@LoadedSoundpackVersion,.,) = %Replace(@Version,.,)} {
   #Var TestTmp1 {@TestTmp1%{CRLF}+ Scripts for your soundpack version have been loaded according to the version checker.}
  } {
   #Var TestTmp1 {@TestTmp1%{CRLF}- Scripts for your soundpack version have not been loaded according to the version checker. Try type SPRELOAD to fix this.}
  };
  #If {%Defined(DeclaredVariables)=1 and %Defined(PreservedVariables)=1} {
   #Var TestTmp1 {@TestTmp1%{CRLF}+ The Variables framework seems to be loaded.}
  } {
   #Var TestTmp1 {@TestTmp1%{CRLF}- The Variables framework does not seem to have been loaded. Try type SPRELOAD to fix this.}
  }
 } {
  #Var TestTmp1 {@TestTmp1%{CRLF}- Main.set variables missing. Aborting the test.}
 };
 #Var TestTmp2 {%{CRLF}-};
 #If {%Pos(@TestTmp2,@TestTmp1)>0} {
  #Var TestTmp1 {@TestTmp1%CRLF  The testing tool detected one or more issues with the soundpack.}
 } {
  #Var TestTmp1 {@TestTmp1%CRLF  The testing tool could not find any issues with the soundpack.}
 };
 #If {%1="copy"} {
  #Clip {@TestTmp1};
  #Say {Soundpack test report has hopefully been copied to the clipboard.}
 } {
  #Say {@TestTmp1}
 };
 #UnVar TestTmp1;
 #UnVar TestTmp2
} {}
#Alias SPReload {
 #If {%1<>"1"} {#Say {Reloading}};
 #Ignore Key On;
 #Ignore Trigger On;
 #If {@AutoSave=1} {ConfSaveAll Quiet};
 #PC 0 Stop;
 #UnAlarm -1;
 #Alarm 0 {
  #Exec {
   #Reload;
   #Var CharacterName {@CharacterName};
   #Var Classification {@Classification};
   #Var Connected {@Connected};
   #Var Exits {@Exits};
   #Var Hostile {@Hostile};
   #Var InSpace {@InSpace};
   #Var Light {@Light};
   #Var HistoryLogBuffer {@HistoryLogBuffer};
   #Var HistoryLogInitialization {@HistoryLogInitialization};
   #Var RoomName {@RoomName};
   #Var RoomSound {@RoomSound};
   #Var Powered {@Powered};
   #Var ZoneName {@ZoneName};
   #Var ZoneType {@ZoneType};
   SPLoad1 0
  }
 }
} {}
#Key {Control-S} {
 #SetSpeech incoming stop;
 ConfSaveAll
} {}
#Trigger {[[]1] Connect as *.} {
 #Var CharacterName %1;
 #If {@BypassLoginMenu=1} {
  #If {%Defined(LoadTmp2)=0} {#Exec {1}} {#Var BypassLoginMenuPostponed 1}
 }
} {Main}

#Alias SPPrompt {
 #If {%Length(%3)=0} {
  #If {%Replace(%1,~",)="Disable"} {
   #UnVar SPPromptCaption;
   #UnVar SPPromptOptions;
   #UnVar SPPromptCallback;
   #UnVar SPPromptCallbackLastArg;
   #UnVar SPPromptAwaitingUserInput;
   #UnVar SPPromptAcceptBlankUserInput;
   #Class ShowSPPrompt Disable
  } {
   #Exec {>SPPrompt%RTrim( %0)}
  }
 } {
  #Var SPPromptCaption {%1};
  #If {%Length(@SPPromptCaption)>0} {
   #If {%IfWord(%Right(@SPPromptCaption,1),. : ! ?, )=0} {#Var SPPromptCaption {@SPPromptCaption:}};
   #Say {@SPPromptCaption} NoDisplay
  };
  #If {%Right(%2,2)=%CRLF} {#Var SPPromptOptions {%Copy(%2,1,%Length(%2)-2)}} {#Var SPPromptOptions {%2}};
  #If {%Left(@SPPromptOptions,2)=%CRLF} {#Var SPPromptOptions {%Copy(@SPPromptOptions,3)}};
  #If {%Pos(:,%3)=1} {
   #Var SPPromptAcceptBlankUserInput 1;
   #Var SPPromptCallback {%Copy(%3,2)}
  } {
   #Var SPPromptAcceptBlankUserInput 0;
   #Var SPPromptCallback {%3}
  };
  #Var SPPromptCallbackLastArg {%4};
  #UnVar SPPromptTmp1;
  #UnVar SPPromptAwaitingUserInput;
  #Class ShowSPPrompt Enable;
  #Config DisplayCommandsInOutput False;
  #Exec { };
  #Exec {>secho};
  #If {@CommandEcho=1} {#Config DisplayCommandsInOutput True}
 }
} {}
#Trigger {What would you like to be repeated back to you using soundpack echo?} {
 #Class ShowSPPrompt Disable;
 #If {%Defined(SPPromptCaption)=0} {
  SPPrompt Disable
 } {
  #Var SPPromptAwaitingUserInput 1;
  #If {%Length(@SPPromptOptions)>0} {
   #If {%Length(@SPPromptCaption)>0} {#Say {  @SPPromptCaption} NoVoice};
   #Sub {@SPPromptOptions}
  } {
   #GagLine 1 Voice;
   #Sub {@SPPromptCaption}
  }
 }
} {ShowSPPrompt}

#Alias soundpack {
 #Config DisplayCommandsInOutput False;
 #If {%1="register"} {#Exec {>#$#register_soundpack @SoundpackName | @Version}};
 #If {%1="unregister"} {#Exec {>#$#unregister_soundpack}};
 #If {%1="version"} {#Exec {>#$#soundpack_version_query @SoundpackName}};
 #If {@CommandEcho=1} {#Config DisplayCommandsInOutput True};
 #If {%1="reload"} {
  #Say {Reloading the soundpack... Please remain calm.};
  SPReload 1
 }
} {}
#Trigger {Your soundpack appears to be out of date! You're using version '*', but the most recent version is '*'. You can download the most recent version from: *} {
 #Var NewVersion {%2};
 #Var NewMajorVersion {%Word(@NewVersion,.,1)};
 #Var NewMinorVersion {%Word(@NewVersion,.,2)};
 #Var NewPatchVersion {%Word(@NewVersion,.,3)};
 #If {@NewMajorVersion>@MajorVersion or @NewMinorVersion>@MinorVersion or @NewPatchVersion>@PatchVersion} {
  #If {@AlertOnUpdate=1 or @NewMajorVersion>@MajorVersion} {
   #If {@NewMajorVersion>@MajorVersion} {
    msplay {General\Misc\UpdateAlert@Ext}
   } {
    msplay {General\Misc\MOTD@Ext}
   };
   #If {@NewPatchVersion>@PatchVersion} {#Var UpdateTmp1 {There's a patch update available for your soundpack.}};
   #If {@NewMinorVersion>@MinorVersion} {#Var UpdateTmp1 {There is a minor update available for your soundpack.}};
   #If {@NewMajorVersion>@MajorVersion} {#Var UpdateTmp1 {There is a major update available for your soundpack!}};
   #Var UpdateTmp1 {*** @UpdateTmp1 ***
If you're running the soundpack from the actively maintained shared folder in Dropbox, then you should make sure your Dropbox is up to date and then type SPRELOAD or reconnect to Miriani. If not, you should consider downloading the latest version from:
%3
The latest version is @NewVersion.
You are currently using version @Version of the @SoundpackName.};
   #If {@NewMajorVersion=@MajorVersion} {#Var UpdateTmp1 {@UpdateTmp1%{CRLF}If you don't want to see these update alerts in the future, then you can disable soundpack update notifications in conf gen.}};
   #Sub {@UpdateTmp1};
   #UnVar UpdateTmp1
  } {
   #GagLine 1 All
  }
 } {
  #GagLine 1 All
 };
 #UnVar NewMajorVersion;
 #UnVar NewMinorVersion;
 #UnVar NewPatchVersion;
 #UnVar NewVersion
} {Main}
#Trigger {Your soundpack has been updated to version '*'! You can download it here: *} {
 #Var NewVersion {%1};
 #Var NewMajorVersion {%Word(@NewVersion,.,1)};
 #Var NewMinorVersion {%Word(@NewVersion,.,2)};
 #Var NewPatchVersion {%Word(@NewVersion,.,3)};
 #If {@NewMajorVersion>@MajorVersion or @NewMinorVersion>@MinorVersion or @NewPatchVersion>@PatchVersion} {
  #If {@AlertOnUpdate=1 or @NewMajorVersion>@MajorVersion} {
   #If {@NewMajorVersion>@MajorVersion} {
    msplay {General\Misc\UpdateAlert@Ext}
   } {
    msplay {General\Misc\MOTD@Ext}
   };
   #If {@NewPatchVersion>@PatchVersion} {#Var UpdateTmp1 {There's a patch update available for your soundpack.}};
   #If {@NewMinorVersion>@MinorVersion} {#Var UpdateTmp1 {There is a minor update available for your soundpack.}};
   #If {@NewMajorVersion>@MajorVersion} {#Var UpdateTmp1 {There is a major update available for your soundpack!}};
   #Var UpdateTmp1 {*** @UpdateTmp1 ***
If you're running the soundpack from the actively maintained shared folder in Dropbox, then you should make sure your Dropbox is up to date and then type SPRELOAD or reconnect to Miriani. If not, you should consider downloading the latest version from:
%2
The latest version is @NewVersion.
You are currently using version @Version of the @SoundpackName.};
   #If {@NewMajorVersion=@MajorVersion} {#Var UpdateTmp1 {@UpdateTmp1%{CRLF}If you don't want to see these update alerts in the future, then you can disable soundpack update notifications in conf gen.}};
   #Sub {@UpdateTmp1};
   #UnVar UpdateTmp1
  } {
   #GagLine 1 All
  }
 } {
  #GagLine 1 All
 };
 #UnVar NewMajorVersion;
 #UnVar NewMinorVersion;
 #UnVar NewPatchVersion;
 #UnVar NewVersion
} {Main}

#Alias LoadExtras {
 #ForAll {%Replace(@ExtraScriptFiles,%CRLF,|)} {
  #If {%Pos(^,%i)>0} {LoadExtra {%Word(%i,^,1)} {%1}}
 }
} {}
#Alias LoadExtra {
 #Var ExtraTmp1 {LoadExtraScript%1};
 #If {%Defined(@ExtraTmp1)=1} {
  #If {@{@ExtraTmp1}=1} {
   #If {%IfWord(Quiet,%2,|)=0} {#Say {Loading the %1 extra.}};
   #Load {@ScriptPath\Extras\%1.set}
  } {
   #If {%IfWord(NoUnload,%2,|)=0} {
    #If {%IfWord(Quiet,%2,|)=0} {#Say {Unloading the %1 extra.}};
    #UnLoad {@ScriptPath\Extras\%1.set}
   }
  }
 };
 #UnVar ExtraTmp1
} {}

#If {%Defined(DeclaredVariables)=0} {#Var DeclaredVariables {}}
#If {%Defined(PreservedVariables)=0} {#Var PreservedVariables {}}

#Alias MakeVarName {
 #If {%Length(%1)=0} {#Var VarNameTmp2 {Tmp}} {#Var VarNameTmp2 {%1}};
 #If {%Length(%2)=0} {#Var VarNameTmp2 {VarName}} {#Var VarNameTmp2 {%2}};
 #Var VarNameTmp1 {%Replace(%1,~",)};
 #Var VarNameTmp1 {%Replace(@VarNameTmp1,~~,)};
 #Var VarNameTmp1 {%Replace(@VarNameTmp1, ,0)};
 #Var {@VarNameTmp2} {};
 #Var VarNameTmp3 0;
 #Var VarNameTmp4 {%Length(@VarNameTmp1)};
 #While {@VarNameTmp3<@VarNameTmp4} {
  #Math VarNameTmp3 {@VarNameTmp3+1};
  #If {%Copy(@VarNameTmp1,@VarNameTmp3,1)=*"[a-z0-9]"} {#Var {@VarNameTmp2} {@{@VarNameTmp2}%Copy(@VarNameTmp1,@VarNameTmp3,1)}}
 };
 #Var {@{@VarNameTmp2}} {};
 #UnVar VarNameTmp1;
 #UnVar VarNameTmp2;
 #UnVar VarNameTmp3;
 #UnVar VarNameTmp4
} {}
#Alias PreserveVariable {
 #If {%IfWord(%1,@PreservedVariables,|)=0} {#Var PreservedVariables {@PreservedVariables|%1}}
} {}
#Alias SetPreservedVariable {
 #If {%IfWord(%1,@PreservedVariables,|)=0} {#Var PreservedVariables {@PreservedVariables|%1}};
 #Var {%1} {%2}
} {}
#Alias UnsetPreservedVariables {
 #Var VarTmp1 {};
 #ForAll {@PreservedVariables} {
  #If {%Length(%i)>0} {
   #If {%i=*%1} {
    #Exec {#UnVar {%i}}
   } {
    #Var VarTmp1 {@VarTmp1|%i}
   }
  }
 };
 #Var PreservedVariables {@VarTmp1};
 #UnVar VarTmp1
} {}
