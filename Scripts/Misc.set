#Alias rem {#Exec {remove%RTrim( %0)}}
#Trigger {*--*} {
 #If {%Length(%Trim(%Replace(%0,-,)))=0} {
  #If {@GagDashes=1 and (@Main<-9 or @Main>0)} {#GagLine 1 All};
  #If {@Main>0 and %Defined(Scanning)=1 and @ScanOutputLabelsDetected>0} {
   ScanProcessOutput;
   #If {%Defined(ScanFilterVar)=1} {
    #UnGagLine;
    #If {%Length(@{ScanObject@{ScanFilterVar}Raw})>0} {
     #Var ScanFormatOutput {@{ScanObject@{ScanFilterVar}Raw}}
    } {
     #Var ScanFormatOutput {It has no @ScanFilter.}
    };
    #If {@InterruptOnFilteredScans=1 or (@InterruptOnScanningStarships=1 and @ScanObjectObjectTypeRaw="Starship")} {Speech Stop};
    #Sub {@ScanFormatOutput}
   } {
    #If {@ScanFilters>0} {
     #UnGagLine;
     ScanFormat {@ScanObjectObjectTypeRaw};
     #If {@InterruptOnScanningStarships=1 and @ScanObjectObjectTypeRaw="Starship"} {Speech Stop};
     #Sub {@ScanFormatOutput@ScanFormatExtraOutput}
    }
   }
  };
  #If {@Main>0 and %Defined(DamageReader)=1} {#Class DamageReader Disable;#UnVar DamageReader}
 }
} {}

#Alias lag {
 #Exec {>lag%RTrim( %0)};
 #If {%1=""} {
  #Class LagTest Enable;
  #Conf DisplayCommandsInOutput False;
  #Exec {>#$#soundpack_ping};
  #If {@CommandEcho=1} {#Conf DisplayCommandsInOutput True}
 }
} {}
#Trigger {There is currently an estimated * of server CPU lag and *} {
 #Class LagTest Disable;
 #Var ShowPingPongLagReport {};
 #Sub {There is currently an estimated %1 of server processing lag.}
} {LagTest}
#Alias PassThru {
#if {@PassThru=1} {
#Var PassThru 0;
#configure ExpandCommandLineVars True
} {
#var PassThru 1;
#Configure ExpandCommandLineVars False
};
ConfPrint {Bypass mode %if(@PassThru = 1,enabled,disabled).}
} {}
#Trigger {(* second* roundtime.)} {
 #If {%IsNumber(%1)=1} {
  #If {@PlayRoundtimeSound=1} {
   #Alarm {%eval(%1-1)} {msplay ship\computer\beep@Ext}
  };
  #If {%1<@RoundTimeThreshold} {
   #GagLine 1 Voice
  };
  ExpPipe {%0} {(%1 sec%2 roundtime.)} {%1 sec%2 roundtime};
  #Sub {@ExpString}
 }
} {Main}
#Trigger {Announcement*: *} {
 msplay General\Misc\Announcement@Ext;
 PushMessage Announcements {%0}
} {Main}
#Trigger {Game Change: *} {
 #Sub {[Game Change]: %1};
 msplay General\Misc\GameChange*2@Ext;
 PushMessage Changelog %0
} {Main}
#Trigger {That exit appears to be blocked.} {msplay General\Misc\CantGo*5@Ext} {Main}
#Trigger {* crumple* up and throw* it away.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\Paper*3@Ext
 }
} {Main}
#Trigger {* attempts to board *, but is turned away by *.} {msplay General\Misc\CantGo*5@Ext} {Main}
#Trigger {* suddenly steps from the background.} {msplay General\Misc\HostIn*3@Ext;
ExpPipe {%0} {%0} {%1 appears};
#sub {@ExpString}} {Main}
#Trigger {* coalesces into being nearby.} {msplay General\Misc\HostIn*3@Ext;
ExpPipe {%0} {%0} {%1 appears};
#sub {@ExpString}} {Main}
#Trigger {* suddenly seems to disappear into the background.} {msplay General\Misc\HostOut*3@Ext;
ExpPipe {%0} {%0} {%1 disappears};
#sub {@ExpString}} {Main}
#Trigger {* dissolves into billions of constituent particles and disperses into a hitherto unnoticed breeze.} {msplay General\Misc\HostOut*3@Ext;
ExpPipe {%0} {%0} {%1 disappears};
#sub {@ExpString}} {Main}
#Trigger {I don't understand that.} {
 #If {@GagDontUnderstandMessages=1} {#GagLine 1 All};
 msplay General\Misc\Command*10@Ext;
 #If {@Main<0} {MainTrigSwitch 1}
} {}
#Trigger {*[[]Enter "yes" or "no"]} {
 msplay General\Misc\prompt@Ext;
 ExpPipe {%0} {%0} {%1[Y/N]};
 #Sub {@ExpString}
} {Main}
#Trigger {Your choice:} {msplay General\Misc\prompt@Ext} {Main}
#Trigger {Enter your selection[.:]} {msplay General\Misc\prompt@Ext} {Main}
#Trigger {[[]Type a line of input or [`]~~@abort' to abort the command.]} {
 #If {@Main<0 or %IfWord(%LastLine(1),Enter your selection.|Enter your selection:|Your choice:,|)=0} {
  msplay General\Misc\prompt@Ext;
  ExpPipe {%0} {%0} {:Voice:Type a line of input, use ~@abort to cancel.};
  #Sub {@ExpString}
 } {
  #GagLine 1 All
 };
 #If {@Main<0} {MainTrigSwitch 1}
} {}
#Trigger {[[]Type lines of input; *]} {
 #If {@ConfPrintQuietly=1} {
  #Var ConfPrintQuietly 0
 } {
  msplay General\Misc\prompt@Ext;
  ExpPipe {%0} {%0} {:Voice:%0};
  #Sub {@ExpString};
  #If {@Main<0} {MainTrigSwitch 1}
 }
} {}
#Trigger {>> Command Aborted <<} {
 msplay General\Misc\Cancel@Ext;
 ExpPipe {%0} {Aborted.} {:Voice:Aborted.};
 #Sub {@ExpString};
 #If {@Main<0} {MainTrigSwitch 1}
} {}
#Trigger {Invalid selection.} {
 msplay General\Misc\Cancel@Ext;
 #If {@Main<0} {MainTrigSwitch 1}
} {}

#Trigger {* gestures for you to join * group, which you promptly do.} {
 ExpPipe {%0} {%0} {%1 adds you to %2 group.};
 #sub {@ExpString};
 msplay General\Misc\joins@Ext
} {Main}
#Trigger {You gesture towards *, who promptly joins your group.} {
 msplay General\Misc\joins@ext;
 ExpPipe {%0} {%0} {You add %1 to your group.};
 #sub {@ExpString}
} {Main}
#Trigger {* indicates that * no longer wishes you to follow *.*} {msplay General\Misc\disband@Ext} {Main}
#Trigger {* leaves your group.*} {msplay General\Misc\disband@Ext} {Main}
#Trigger {A small lock destroying explosive suddenly explodes, *.} {msplay Ship\Misc\HatchBomb@Ext} {Main}

#Trigger {*You carefully place * along the edge of * hatch. You press a small button and step back as the charge begins its * second countdown*} {msplay Ship\Misc\HatchBombPlace@Ext} {Main}
#Trigger {*You hear*strange sound* coming from the airlock*} {msplay Ship\Misc\HatchBombPlaceHere@Ext} {Main}
#Trigger {*You hear what sounds like an explosion coming from the general area of the airlock*} {msplay Ship\Misc\HatchBombHere@Ext} {Main}
#Trigger {* hear a deep rumbling as the docking bay door *s.*} {
#if {%2="open" OR %2="close"} {
msplay Ship\Misc\DockingBayDoor%{2}@Ext
}
} {Main}
#Trigger {* to the * slides *.} {
#var DoorTmp1 {%word(%3, ,1)};
#if {@DoorTmp1="open" OR @DoorTmp1="closed"} {
msplay Ship\Misc\Door@{DoorTmp1}Stateroom@Ext
}
} {Main}
#Trigger {* leading * slides *.} {
#var DoorTmp1 {%word(%3, ,1)};
#if {@DoorTmp1="open" OR @DoorTmp1="closed"} {
msplay Ship\Misc\Door@{DoorTmp1}Stateroom@Ext
}
} {Main}

#Trigger {Directions from * to *: *.} {expPipe {:0:%0} {:0:%1 to %2: %3.} {:0:%2: %3.};
#sub @ExpString} {Main}
#Trigger {* arrives, looking vaguely puzzled.} {msplay General\Misc\DoorChime@Ext} {Main}
#Trigger {* several automated drones begin healing *.} {msplay General\Misc\ITPNStart@Ext} {Main}
#Trigger {* stands up, looking much healthier than before.} {msplay Stand@Ext} {Main}
#Trigger {You follow *.} {
 #Var Following 1;
 #If {%Pos(@OtherName,%1)=1} {#Var Dir {@OtherDir}};
 #If {%Pos(into the ship,%1)<>0} {
  #If {@RoomName<>"Airlock"} {
   msplay Ship\Misc\AirlockEnter@Ext
  }
 } {
  #If {%IfWord(%Word(%1, ,%NumWords(%1, )),@DirList, )=1} {
   MStep You Leave {@OtherDir}
  }
 }
} {Main}
#Trigger {*.*} {
 #If {%Length(%2)=0 and %Pos(:,%0)=0 and %Pos(~,,%0)=0 and %IfWord(%Left(%1,1),>| |-|#|~(|[,|)=0} {
  #Var MovementTestTmp2 {%Word(%1, ,%NumWords(%1, ))};
  #Var MovementTestTmp3 {};
  #If {%IfWord(@MovementTestTmp2,@DirList, )=0} {
   #Var MovementTestTmp1 {%Pos( on a ,%1)};
   #If {@MovementTestTmp1=0} {#Var MovementTestTmp1 {%Pos( on an ,%1)}};
   #If {@MovementTestTmp1>0} {
    #Var MovementTestTmp1 {%Copy(%1,1,%Eval(@MovementTestTmp1-1))};
    #Var MovementTestTmp2 {%Word(@MovementTestTmp1, ,%NumWords(@MovementTestTmp1, ))};
    #If {%IfWord(@MovementTestTmp2,@DirList, )=1} {
     #Var MovementTestTmp3 {%Delete(%1,1,%Eval(%Length(@MovementTestTmp1)+4))};
     #Var MovementTestTmp1 {%Copy(%1,1,%Eval(%Length(@MovementTestTmp1)-%Length(@MovementTestTmp2)-1))}
    } {
     #Var MovementTestTmp2 {}
    }
   } {
    #Var MovementTestTmp2 {}
   }
  } {
   #Var MovementTestTmp1 {%Copy(%1,1,%Eval(%Length(%1)-%Length(@MovementTestTmp2)-1))}
  };
  #If {%Length(@MovementTestTmp2)>0} {
   #Var MovementTmp1 {@MovementTestTmp1};
   #Var MovementDir {@MovementTestTmp2};
   #Var MovementMount {@MovementTestTmp3};
   #Var MovementTmp4 3;
   #If {%Pos(You ,@MovementTmp1)=1} {
    #If {%Pos(You can ,@MovementTmp1)=0} {
     #Var MovementTmp2 {%NumWords(@MovementTmp1, )};
     #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2)};
     #While {@MovementTmp2>1 and @MovementTmp4>0} {
      #Math MovementTmp4 {@MovementTmp4-1};
      #If {%IfWord(@MovementTmp3,@WalkStyles,%CRLF)=1} {
       #Var MovementTmp2 {-1}
      } {
       #Math MovementTmp2 {@MovementTmp2-1};
       #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2) @MovementTmp3}
      }
     };
     #If {@MovementTmp2=-1} {
      #If {@GagTreadLine=1} {#GagLine 1 All};
      MStep You Leave {@MovementDir}
     }
    }
   } {
    #Var MovementTmp2 {%Pos( in from ,@MovementTmp1 )};
    #If {@MovementTmp2>0} {
     #Var MovementTmp1 {%Copy(@MovementTmp1,1,%Eval(@MovementTmp2-1))};
     #Var MovementTmp2 {%NumWords(@MovementTmp1, )};
     #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2)};
     #While {@MovementTmp2>1 and @MovementTmp4>0} {
      #Math MovementTmp4 {@MovementTmp4-1};
      #If {%IfWord(@MovementTmp3,@WalkStyles,%CRLF)=1} {
       #Var MovementTmp2 {-1}
      } {
       #Math MovementTmp2 {@MovementTmp2-1};
       #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2) @MovementTmp3}
      }
     };
     #If {@MovementTmp2=-1} {
      MStep Other Arrive {@MovementDir}
     }
    } {
     #Var MovementTmp2 {%NumWords(@MovementTmp1, )};
     #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2)};
     #While {@MovementTmp2>1 and @MovementTmp4>0} {
      #Math MovementTmp4 {@MovementTmp4-1};
      #If {%IfWord(@MovementTmp3,@WalkStyles,%CRLF)=1} {
       #Var MovementTmp2 {-1}
      } {
       #Math MovementTmp2 {@MovementTmp2-1};
       #Var MovementTmp3 {%Word(@MovementTmp1, ,@MovementTmp2) @MovementTmp3}
      }
     };
     #If {@MovementTmp2=-1} {
      #Var OtherDir {@MovementDir};
      #Var OtherName {%Copy(@MovementTmp1,1,%Eval(%Length(@MovementTmp1)-%Length(@MovementTmp3)-1))};
      MStep Other Leave {@MovementDir}
     }
    }
   };
   #4 {#UnVar {MovementTmp%i}}
  }
 }
} {Main}

#Trigger {Please do not enter so many commands at once.} {msplay General\Misc\spam@Ext} {Main|SpamAlerts}
#Trigger {Spamming weapon commands will get you nowhere. Please be considerate. If you are experiencing lag, it would be best to wait for one command to successfully go through before attempting another. Thank you.} {msplay General\Misc\spam@Ext} {Main|SpamAlerts}
#Trigger {*A* sanitation drone arrives.} {mrandomplay General\Misc\DroneEnters*2@Ext} {Main}
#Trigger {*A * sanitation drone goes *} {mrandomplay General\Misc\DroneLeaves*2@Ext} {Main}
#Trigger {*A * sanitation drone whirls around.} {mrandomplay General\Misc\DroneWhirls*5@Ext} {Main}
#Trigger {*A * sanitation drone collects *} {mrandomplay General\Misc\DroneCollects*2@Ext} {Main}
#Trigger {*A * drone rushes into the room and begins recharging any nearby drones. Just as quickly as it enters, it vanishes.} {ExpPipe {%0} {A %1 drone begins recharging all nearby drones.} {Drones recharged.};
#sub @ExpString;
msplay General\Misc\DroneRecharge@Ext} {Main}
#Trigger {* drags you *} {
 #If {%Word(%2, ,1)<>"off"} {
  #Var Following 1
 }
} {Main}
#Trigger {You seem to have left * behind!} {ExpPipe {%0} {You have left %1.} {Left %1 behind.};
PushMessage Left @ExpString;
#sub @ExpString;
#if {@WarnWhenLeft=1} {
msplay General\Misc\LeftPerson@Ext
}} {Main}
#Trigger {Wait *} {
 #If {%IsNumber(%Word(%1, ,1))=1 and @PlayWhenThwarted=1} {msplay General\Misc\vollimit@Ext 20};
 #If {@Main<0} {MainTrigSwitch 1}
} {}

#Trigger {A* firework launches itself into the air and begins to rapidly expand into a *.} {
 msplay General\Misc\Firework*2@Ext
} {Main}
#Trigger {A bright ring of light emerges from the * and comes to a halt. * steps off.} {
 ExpPipe {%0} {%2 emerges from a lift which comes through the %1.} {%2 emerges from the %1.};
 #Sub @ExpString;
 mwplay General\Misc\HGLift@Ext
} {Main}
#Trigger {* steps on to a small platform on the floor. Sensing * presence, a ring of lights along the perimeter of the circle light up as the platform shoots * out of sight.} {
 ExpPipe {%0} {%1 shoots %3 as the lift senses %2 presence.} {%1 shoots %3.};
 #Sub @ExpString;
 mwplay General\Misc\HGLift@Ext
} {Main}
#Trigger {*ancient pirate chest*here.} {msplay General\Archaeology\practice@Ext} {Main}
#Trigger {* ha* been loaded into your courier ship. Your destination is * in Sector *.} {
 Speech Stop;
 ExpPipe {%0} {%0} {%1 for %3 in %4.};
 #Sub {@ExpString};
} {Main}
#Trigger {* pull* a lever on a* and *the reels start to spin!} {Msplay General\Misc\skeeballrolls@Ext} {Main}
#Trigger {Your droids and drones suddenly stop following you, choicelessly obeying an order programmed into them.} {msplay General\Misc\InstructDrone@Ext} {Main}
#Trigger {You ascend the stairs and push through a set of rotating glass doors.} {
 DontStopSpeaking;
 msplay General\Misc\Door~@Ext
} {Main}
#Trigger {You push through a set of rotating glass doors.} {
 DontStopSpeaking;
 msplay General\Misc\Door~@Ext
} {Main}

#Trigger {You push through a metal turnstile.} {DontStopSpeaking} {Main}
#Trigger {It has been signed by *} {
 #Sub {(%NumWords(%1,~,)) %1}
} {Main}
#trigger {k} {#gagline all} {main}

#Alias typespeed {
 #Var TypeSpeedTmp1 {%Trim(%0)};
 #Var TypeSpeedTmp1 {%Replace(@TypeSpeedTmp1,~",)};
 #Var TypeSpeedTmp1 {%Replace(@TypeSpeedTmp1,~~,)};
 #If {@TypeSpeedTmp1=""} {
  #UnAlarm -1;
  #Alarm 60 {
   #Say {Time's up! Hit enter to see your result.};
   msplay {LaserTag\NoTargets@Ext}
  };
  #Say {typespeed } Entry|NoReturn;
  #Say {Start typing!}
 } {
  #Var TypeSpeedTmp2 {%Pos(%CRLF,%0%CRLF)};
  #Math TypeSpeedTmp2 {@TypeSpeedTmp2-2};
  #Math TypeSpeedTmp3 {@TypeSpeedTmp2/5};
  #Say {Approximately @TypeSpeedTmp3 words per minute!};
  #UnVar TypeSpeedTmp2;
  #UnVar TypeSpeedTmp3
 };
 #UnVar TypeSpeedTmp1
} {}
#Trigger {To the immediate * you see *} {
 ExpPipe {%0} {%0} {%1: %2};
 #Sub {@ExpString}
} {Main}
#Trigger {This *} {
 #Var ShipMiscTmp1 {%Word(%1, ,1)};
 #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~",)};
 #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~~,)};
 #If {%IfWord(@ShipMiscTmp1,moon planet ship station, )=1} {
  #Var ShipMiscTmp1 {%Word(%1, ,2)};
  #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~",)};
  #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~~,)};
  #Var ShipMiscTmp1 {%Replace(@ShipMiscTmp1,~,,)};
  #If {%IfWord(@ShipMiscTmp1,demands transmits surrenders, )=1} {
   msplay Ship\Comm\Sector@Ext
  };
  #If {%IfWord(@ShipMiscTmp1,broadcasts, )=1} {
   msplay Ship\Comm\Broadcast@Ext
  }
 }
} {Main}
#Trigger {* sit* down on *} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\sit@Ext
 }
} {Main}
#Trigger {* stand* up.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\stand@Ext
 }
} {Main}
#Trigger {* pull* to * feet.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\stand@Ext
 }
} {Main}
#Trigger {* yawns suddenly and collapses to the ground, asleep.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay {General\Socials\yawn*2@Ext};
  msplay {General\Socials\collapse*3@Ext}
 }
} {Main}

#Alias who {
 #Class StartReadingWhoList Disable;
 #Class ReadWhoList Disable;
 #Exec {who%RTrim( %0)}
} {StartReadingWhoList}
#Alias whoc {#Exec {whocheck%RTrim( %0)}} {}
#Alias WhoCheck {
 #If {%1="" and %2="ReadingComplete"} {
  #Class ReadWhoList Disable;
  #Var WhoTmp2 {, };
  #Var WhoData {};
  #If {%Defined(LastConnectedPlayers)=1} {
   #Var DisconnectedPlayers {};
   #Var NewConnectedPlayers {};
   #ForAll {@ConnectedPlayers} {
    #If {%IfWord(%i,@LastConnectedPlayers,|)=0} {
     #If {%Length(@NewConnectedPlayers)=0} {
      #Var NewConnectedPlayers {%i}
     } {
      #AddItem NewConnectedPlayers {%i}
     }
    }
   };
   #ForAll {@LastConnectedPlayers} {
    #If {%IfWord(%i,@ConnectedPlayers,|)=0} {
     #If {%Length(@DisconnectedPlayers)=0} {
      #Var DisconnectedPlayers {%i}
     } {
      #AddItem DisconnectedPlayers {%i}
     }
    }
   };
   #If {%Length(@NewConnectedPlayers)>0} {
    #If {%NumWords(@NewConnectedPlayers,|)=1} {
     #Var WhoData {@WhoData%{CRLF}There is 1 player who has connected since last check:}
    } {
     #Var WhoData {@WhoData%{CRLF}There are %NumWords(@NewConnectedPlayers,|) players who have connected since last check:}
    };
    #Var WhoTmp1 {%Sort(@NewConnectedPlayers,0)};
    #Var WhoData {@WhoData%{CRLF}%Replace(@WhoTmp1,|,@WhoTmp2).}
   } {
    #Var WhoData {@WhoData%{CRLF}No players have connected since last check.}
   };
   #If {%Length(@DisconnectedPlayers)>0} {
    #If {%NumWords(@DisconnectedPlayers,|)=1} {
     #Var WhoData {@WhoData%{CRLF}There is 1 player who has disconnected since last check:}
    } {
     #Var WhoData {@WhoData%{CRLF}There are %NumWords(@DisconnectedPlayers,|) players who have disconnected since last check:}
    };
    #Var WhoTmp1 {%Sort(@DisconnectedPlayers,0)};
    #Var WhoData {@WhoData%{CRLF}%Replace(@WhoTmp1,|,@WhoTmp2).}
   } {
    #Var WhoData {@WhoData%{CRLF}No players have disconnected since last check.}
   };
   #UnVar DisconnectedPlayers;
   #UnVar NewConnectedPlayers
  };
  #Var WhoTmp1 {%Sort(@ConnectedPlayers,0)};
  #Var WhoData {@WhoData%{CRLF}@WhoOriginalHeader%{CRLF}%Replace(@WhoTmp1,|,@WhoTmp2).};
  #Var LastConnectedPlayers {@ConnectedPlayers};
  #UnVar ConnectedPlayers;
  #UnVar WhoOriginalHeader;
  #UnVar WhoTmp1;
  #UnVar WhoTmp2
 } {
  #Class StartReadingWhoList Enable;
  #Exec {>who};
  #If {%Defined(LastConnectedPlayers)=1} {
   #Say {Checking...} NoDisplay
  } {
   #Say {First check.} NoDisplay
  };
  #Alarm {0@{DecimalChar}2} {
   #Conf DisplayCommandsInOutput False;
   #Exec {>#$#soundpack_ping};
   #If {@CommandEcho=1} {#Conf DisplayCommandsInOutput True}
  }
 }
} {}

#Trigger {There are * players connected, * of whom have been active recently:} {
 #GagLine 1 All;
 #Var WhoOriginalHeader {%0};
 #Var ConnectedPlayers {};
 #Class ReadWhoList Enable;
 #Class StartReadingWhoList Disable
} {Main|StartReadingWhoList}
#Trigger {* (*} {
 #GagLine 1 All;
 #ForAll {%Replace(%0,~),|)} {
  #If {%Pos(~(,%i)>0} {
   #Var WhoTmp1 {%Word(%i,~(,1)};
   #If {%NumWords(@WhoTmp1,])>1} {
    #Var WhoTmp1 {%Word(@WhoTmp1,],2)}
   };
   #Var WhoTmp1 {%Trim(@WhoTmp1)};
   #If {%Left(@WhoTmp1,1)="*" or %Left(@WhoTmp1,1)="^"} {
    #Var WhoTmp1 {%Delete(@WhoTmp1,1,1)}
   };
   #If {%Length(@ConnectedPlayers)=0} {#Var ConnectedPlayers {@WhoTmp1}} {#AddItem ConnectedPlayers {@WhoTmp1}}
  }
 }
} {Main|ReadWhoList}
#Trigger {* - Out of *} {#GagLine 1 All} {Main|ReadWhoList}

#Alias Speech {
 #If {%1="stop"} {
  #If {@UseSpeechInterrupts=1 AND @GagEverything=0} {#SetSpeech Incoming Stop}
 } {
  #If {%IfWord(%1,on off, )=1} {
   #SetSpeech Incoming {%1};
   #SetSpeech VoiceOnly {%1};
   #If {%IfWord(Quiet,%2,|)=0} {#Say {Speech %Proper(%1).} VoiceOnly}
  } {
   #If {%1="set"} {
    #If {%IfWord(%2,@SpeechTypes,|)=1} {
     #If {%2="NVDA"} {
      #Var SpeechTmp1 {SystemAccess}
     } {
      #Var SpeechTmp1 %2
     };
     #If {%2="SAPI"} {
      #ForAll @WindowTypes {#SetSpeech %i @SpeechTmp1 @SAPIVoice @SAPIVolume @SAPIRate}
     } {
      #ForAll @WindowTypes {#SetSpeech %i {@SpeechTmp1}}
     };
     #If {%3<>1} {#Say {Speech Type set to %2.}};
     #UnVar SpeechTmp1
    } {
     #Say {Syntax: Speech Set @SpeechTypes}
    }
   }
  }
 }
} {}
#Alias dnd {
 #If {%Length(%1)>0} {
  #Var Silent {%IfWord(%1,on 1 enable enabled, )}
 } {
  #Var Silent {%Eval(Not @Silent)}
 };
 #Exec {#Alarm 0 {
  MasterMute {@Silent} Quiet;
  Speech {%If(@Silent=1,off,on)} Quiet;
  #If {%IfWord(Quiet,%2,|)=0} {
   #Say {%If(@Silent=1,Enabled,Disabled) Do Not Disturb.}
  }
 }}
} {}

#Trigger {You open * lockbox*.} {msplay general\misc\lockboxopen@Ext} {Main}
#Trigger {You close * lockbox *.} {msplay general\misc\lockboxclose@Ext} {Main}

#Trigger {With a gentle jolt, the ferry*} {
 msplay general\misc\ferry@Ext;
 #If {@SoundIsPlaying=1} {#Var FerryHandle %PlayHandle}
} {Main}
#Trigger {You barely notice as the ferry makes contact with the dock and is securely tied in place*} {
 msplay general\misc\ferrystop@Ext;
 #If {@SoundIsPlaying=1} {#Var FerryHandle %PlayHandle}
} {Main}
#Trigger {You watch as a ferry arrives and gets secured to the dock.} {
 msplay general\misc\ferrystop@Ext
} {Main}
#Trigger {You disembark.} {
 #If {%Defined(FerryHandle)=1} {
  #PC @FerryHandle stop;
  #UnVar FerryHandle
 } {
  msplay General\Misc\Disembark@Ext
 }
} {Main}
#Trigger {You follow * to * Large Wooden Dock.} {
 #Var Following 1;
 #If {%Defined(FerryHandle)=1} {
  #PC @FerryHandle stop;
  #UnVar FerryHandle
 }
} {Main}
#Trigger {* unclip* from *.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\Belt1@Ext
 }
} {Main}
#Trigger {* clip* to *.} {
 #If {%Pos(:,%0)=0 and %Pos(~, ~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0} {
  msplay General\Misc\Belt2@Ext
 }
} {Main}
#Trigger {* knock* on the *.} {
 #If {%Pos(~, seeming vaguely puzzled,%0)=0 and %Pos(:,%0)=0 and %IfWord(%Left(%0,1),>| |[|~(|-,|)=0} {
  msplay General\Misc\Knock1@Ext
 }
} {Main}
#Trigger {* impatiently knock* on the *.} {msplay General\Misc\Knock2@Ext} {Main}
#Trigger {* kick* furiously on the *.} {msplay General\Misc\Knock3@Ext} {Main}
#Trigger {You * credit*} {
 #If {%IfWord(%Word(%1, ,1),earn|receive,|)=1} {
  msplay General\Misc\Credits*4@Ext
 }
} {Main}
#Trigger {You are seated in a chair with several syringes on robotic arms around it.  *} {msplay General\Misc\MedicalOpperation@Ext} {Main}
