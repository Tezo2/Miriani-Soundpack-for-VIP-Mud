#Exec {
SetVariable RelativeTextAtSaTmpX 0;
SetVariable RelativeTextAtSaTmpY 0;
SetVariable RelativeTextDir {};
SetVariable AtSaTmp*5 0
}
#Alias CalculateRelativeTextDir {
 #if {%IsNumber(%1)=0 OR %IsNumber(%2)=0 OR %IsNumber(%3)=0 OR %IsNumber(%4)=0} {
  ConfPrint {Syntax: CalculateRelativeNumericDir TargetX TargetY CurrentX CurrentY};
  #abort
 };
 #math RelativeTextAtSaTmpX {%max(%1,%3) - %min(%1,%3)};
 #math RelativeTextAtSaTmpY {%max(%2,%4) - %min(%2,%4)};
 #var RelativeTextDir {%if(@RelativeTextAtSaTmpX > 0,@RelativeTextAtSaTmpX%if(%3 > %1,W,E),), %if(@RelativeTextAtSaTmpY > 0,@RelativeTextAtSaTmpY%if(%4 > %2,N,S),)}
} {}


; Atmospheric salvaging, v2.1.2
; Written and contributed by Magix.

;   Changelog
;
; 2.2.1:
;  - Added the $R formatter to the Object string.
; 2.2.0:
;  - The output may now be altered by using strings with formatters, see "Conf Atsm".
;  - Added a conf option to set the sort order of objects.
;  - Configuration of this script has been moved to the conf manager.
; 2.1.2:
;  - RemainingStorageCapacityDescription2 will now convert the word "units" to "unit" in the same way as the DistanceToShipDescription2 does.
;  - Removed the converting of "units" to "unit" in DistanceToShipDescription1.
;  - Removed the feature that replaced " are " with " is " in the AtSaDistanceToShipDescription* variables.
; 2.1.1:
;  - Added compatibility with AlterCharge.
; 2.1.0:
;  - Added configuration options to customize the output, see conf.set in the soundpack.
;  - Changed the output method from #Say to #Sub.
;  - Added a new line separator between objects and other starmap information.
;  - Adjusted the power estimate to be more accurate.
;  - AtSa.set is now integrated in the Monkey Term 7 Soundpack Port for VipMud.
; 2.0.1:
;  - Fixed an odd bug which appeared in some clients.
; 2.0.0:
;  - Objects will now be sorted in ascending order.
;  - Changed the power status value to be an estimate of how much power you can safely use for scooping,
;    rather than the regular 100 to 0 percent value.
; 1.0.0:
;  - Initial version, released as a standalone extension.


; The variables below are for declaration only and should not be modified in this file.
#Exec {
SetVariable AtSaCurXCoord 0;
SetVariable AtSaCurYCoord 0;
SetVariable AtSaDistanceToShip 0;
SetVariable AtSaPower 0;
SetVariable AtSaPowerTotal 0;
SetVariable AtSaCargoFilled 0;
SetVariable AtSaRemainingStorageCapacity 0;
SetVariable AtSaTotalStorageCapacity 0;
SetVariable AtSaObjects {}
}
#Var AtSaInformationVars {X:CurXCoord|Y:CurYCoord|Dist:DistanceToShip|Power:PowerTotal|PowerEst:Power|Cargo:CargoFilled|CargoRem:RemainingStorageCapacity|CargoTotal:TotalStorageCapacity}

#Alias {atsm} {
 #Exec {>sm};
 #Class AtSaStarmap Enable
} {}

#Trigger {Atmosphere of *} {#GagLine 4 All} {AtSaStarmap}
#Trigger {Power Remaining: &{AtSaPowerTotal}%} {} {AtSaStarmap}
#Trigger {Cargo Capacity: */*} {
 #If {%IsNumber(%1%2)} {
  #Math AtSaRemainingStorageCapacity {%2-%1};
  #Var AtSaCargoFilled %1;
  #Var AtSaTotalStorageCapacity %2
 }
} {AtSaStarmap}
#Trigger {Distance to Ship: &{AtSaDistanceToShip} unit*} {
 #Math AtSaPower {@AtSaPowerTotal-(@AtSaDistanceToShip*3+2)}
} {AtSaStarmap}
#Trigger {Current Coordinates: *, *} {
 #If {%IsNumber(%1%2)} {
  #Var AtSaCurXCoord %1;
  #Var AtSaCurYCoord %2;
  #Var AtSaTmp1 {@AtSaInformationString};
  #Var AtSaTmp2 %NumWords(@AtSaInformationVars,|);
  #Var AtSaTmp5 {s};
  #While {@AtSaTmp2>0} {
   #Var AtSaTmp3 {%Word(@AtSaInformationVars,|,@AtSaTmp2)};
   #Var AtSaTmp4 {@{AtSa%Word(@AtSaTmp3,:,2)}};
   #Var AtSaTmp3 {%Word(@AtSaTmp3,:,1)};
   #If {@AtSaTmp4=1} {
    #Var AtSaTmp1 {%Replace(%Replace(@AtSaTmp1,$@AtSaTmp5@AtSaTmp3,),$@AtSaTmp3,@AtSaTmp4)}
   } {
    #Var AtSaTmp1 {%Replace(%Replace(@AtSaTmp1,$@AtSaTmp5@AtSaTmp3,@AtSaTmp5),$@AtSaTmp3,@AtSaTmp4)}
   };
   #Math AtSaTmp2 {@AtSaTmp2-1}
  };
  #Sub {@AtSaTmp1}
 }
} {AtSaStarmap}
#Trigger {Objects: *} {
 #Var AtSaObjects {};
 #Var AtSaTmp3 1;
 #Var AtSaTmp4 1;
 #Var AtSaTmp5 %NumWords(%1,~();
 #While {@AtSaTmp4<@AtSaTmp5} {
  #Math AtSaTmp4 {@AtSaTmp4+1};
  #Var AtSaTmp1 {%Word(%1,"(",@AtSaTmp4)};
  #Math AtSaTmp2 {%Pos(")",@AtSaTmp1)-1};
  #Var AtSaTmp1 {%Replace(%Left(@AtSaTmp1,@AtSaTmp2),~,,)};
  #If {%IsNumber(%Replace(@AtSaTmp1, ,))} {
   #Var AtSaTmp2 {%Word(@AtSaTmp1, ,2)};
   #Var AtSaTmp1 {%Word(@AtSaTmp1, ,1)};
   #If {@AtSaTmp1<>@AtSaCurXCoord or @AtSaTmp2<>@AtSaCurYCoord} {
    #Var AtSaTmp8 {%Replace(%Replace(@AtSaObjectString,$XC,@AtSaTmp1),$YC,@AtSaTmp2)};
    #If {@AtSaTmp3>1} {#Var AtSaObjects {@AtSaObjects|}};
    #If {@AtSaTmp1<>@AtSaCurXCoord} {
     #If {@AtSaTmp1>@AtSaCurXCoord} {
      #Math AtSaTmp6 {@AtSaTmp1-@AtSaCurXCoord};
      #Var AtSaTmp8 {%Replace(@AtSaTmp8,$XD,@{AtSaTmp6}E)}
     } {
      #Math AtSaTmp6 {@AtSaCurXCoord-@AtSaTmp1};
      #Var AtSaTmp8 {%Replace(@AtSaTmp8,$XD,@{AtSaTmp6}W)}
     }
    } {
     #Var AtSaTmp6 0;
     #Var AtSaTmp8 {%Replace(@AtSaTmp8,$XD,)}
    };
    #If {@AtSaTmp2<>@AtSaCurYCoord} {
     #If {@AtSaTmp2>@AtSaCurYCoord} {
      #Math AtSaTmp7 {@AtSaTmp2-@AtSaCurYCoord};
      #Var AtSaTmp8 {%Replace(@AtSaTmp8,$YD,@{AtSaTmp7}S)}
     } {
      #Math AtSaTmp7 {@AtSaCurYCoord-@AtSaTmp2};
      #Var AtSaTmp8 {%Replace(@AtSaTmp8,$YD,@{AtSaTmp7}N)}
     }
    } {
     #Var AtSaTmp7 0;
     #Var AtSaTmp8 {%Replace(@AtSaTmp8,$YD,)}
    };
    #If {%Min(@AtSaTmp6,@AtSaTmp7)=0} {
     #Var AtSaTmp9 %Pos($R,@AtSaTmp8);
     #While {@AtSaTmp9>0} {
      #Var AtSaTmp10 %Pos($R,%Delete(@AtSaTmp8,1,@AtSaTmp9));
      #If {@AtSaTmp10>0} {
       #Var AtSaTmp8 {%Delete(@AtSaTmp8,@AtSaTmp9,%Eval(@AtSaTmp10+2))};
       #Var AtSaTmp9 %Pos($R,@AtSaTmp8)
      } {
       #Var AtSaTmp8 {%Left(@AtSaTmp8,%Eval(@AtSaTmp9-1))};
       #Var AtSaTmp9 0
      }
     }
    } {
     #Var AtSaTmp8 {%Replace(@AtSaTmp8,$R,)}
    };
    #If {%Max(@AtSaTmp6,@AtSaTmp7)>9} {
     #Var AtSaObjects {@AtSaObjects%Max(@AtSaTmp6,@AtSaTmp7) @AtSaTmp8}
    } {
     #Var AtSaObjects {@{AtSaObjects}0%Max(@AtSaTmp6,@AtSaTmp7) @AtSaTmp8}
    };
    #Math AtSaTmp3 {@AtSaTmp3+1}
   }
  }
 };
 #If {@AtSaTmp3>1 and @AtSaSortOrder>-1} {
  #Var AtSaObjects {%Sort(@AtSaObjects,@AtSaSortOrder)};
  #Var AtSaTmp4 1;
  #Var AtSaTmp5 {};
  #While {@AtSaTmp3>@AtSaTmp4} {
   #If {@AtSaTmp4=1} {
    #Var AtSaTmp5 {@AtSaTmp5%Delete(%Word(@AtSaObjects,|,@AtSaTmp4),1,3)}
   } {
    #Var AtSaTmp5 {@AtSaTmp5@AtSaBetweenObjects%Delete(%Word(@AtSaObjects,|,@AtSaTmp4),1,3)}
   };
   #Math AtSaTmp4 {@AtSaTmp4+1}
  };
  #GagLine 1 All;
  #Var ConfPrintQuietly 1;
  ConfPrint {@AtSaTmp5}
 } {
  #Sub {No objects}
 };
 #Class AtSaStarmap Disable
} {AtSaStarmap}
#Trigger {You can only scoop from a planet.} {#Class AtSaStarmap Disable} {AtSaStarmap}
#Trigger {Current Coordinates: (*)} {#Class AtSaStarmap Disable} {AtSaStarmap}
#Trigger {I don't understand that.} {#Class AtSaStarmap Disable} {AtSaStarmap}
#TRIGGER {The sounds drop in pitch and eventually cease.} {ExpPipe {%0} {} {};
#sub @ExpString;
msplay General\Asteroid\Trickle@Ext} {Main}
#TRIGGER {Vehicle Status: *} {msplay Ship\Computer\Display@Ext;
ExpPipe {%0} {%1} {%1};
#sub @ExpString} {Main}

#Trigger {A slight breeze enters the cockpit as the storage compartment is opened and closed.} {
 mrandomplay Ship\Misc\Transfer*3@Ext;
 #Gagline 1 all
} {Main}
#Trigger {You hear scrapes and scratching coming from the storage compartment as debris is transferred.} {
 mrandomplay Ship\Misc\Transfer*3@Ext;
 #Gagline 1 all
} {Main}
#Trigger {A series of drones lift canisters of atmospheric debris and cart them off.} {
 mrandomplay Ship\Misc\Transfer*3@Ext;
 #Gagline 1 all
} {Main}
