#Var HistorySoundHandle 0
#If {%Defined(NumberOfBufferedLogMessages)=0} {#Var NumberOfBufferedLogMessages 0}
#If {%Defined(NumberOfMessages)=0} {#var NumberOfMessages 0}
#If {%Defined(HistoryBuffers)=0} {#Var HistoryBuffers {Comm|Output|PA|Say}}
#If {%Defined(CurrentHistoryBuffer)=0} {#var CurrentHistoryBuffer {Output}}
#If {%Defined(CurrentHistoryBufferPosition)=0} {#Var CurrentHistoryBufferPosition 1}

#Alias WriteToLogFile {
 #ForAll {@HistoryBuffers} {
  #If {%i<>""} {
   #If {@{History%{i}LogBufferEntries}>0} {
    #Var HistoryLogTmp1 {@HistoryLogPath%Char - %Trim(%Replace(%i,0, )).log};
    #File 2 {@HistoryLogTmp1};
    #If {%Defined(History%{i}LogInitialization)=1} {
     #Write 2 {.};
     #Close 2;
     #DelFile {@HistoryLogTmp1};
     #File 2 {@HistoryLogTmp1};
     #Write 2 {Log file of %Trim(%Replace(%i,0, )) messages:%CRLF};
     #Exec {#UnVar {History%{i}LogInitialization}}
    };
    #Write 2 {@{History%{i}LogBuffer}};
    #Var {History%{i}LogBuffer} {};
    #Var {History%{i}LogBufferEntries} 0;
    #UnVar HistoryLogTmp1;
    #Close 2
   }
  }
 }
} {}

#Alias PushMessage {
 #If {%IfWord(%1,@IgnoredHistoryBuffers, )=0} {
  #Var {History%{1}Tmp1} {%Replace(%2,|,@CR@CR)};
  #Var {History%{1}Tmp2} {%Replace(@{History%{1}Tmp1},~~,)};
  #Var {History%{1}Tmp2} {%Replace(@{History%{1}Tmp2},~",)};
  #Var {History%{1}Tmp3} {%Replace(@{History%{1}Messages},~~,)};
  #Var {History%{1}Tmp3} {%Replace(@{History%{1}Tmp3},~",)};
  #If {@{History%{1}Tmp3}=""} {
   #Var {History%{1}Tmp4} {}
  } {
   #Var {History%{1}Tmp4} {%Word(@{History%{1}Tmp3},|,%NumWords(@{History%{1}Tmp3},|))}
  };
  #If {@SuppressDuplicateEntries=0 or (@SuppressDuplicateEntries=1 and @{History%{1}Tmp2}<>@{History%{1}Tmp4}) or (@SuppressDuplicateEntries=2 and %IfWord(@{History%{1}Tmp1},@{History%{1}Messages},|)=0)} {
   #Math NumberOfMessages {@NumberOfMessages+1};
   #If {%IfWord(%1,@HistoryBuffers,|)=0} {
    #Var HistoryBuffers {%Sort(@HistoryBuffers|%1,0)}
   };
   #If {%NumWords(@{History%{1}Tmp3},|)>=@HistoryBufferSize} {
    #Var {History%{1}Messages} {%DelNItem(@{History%{1}Messages},1)|@{History%{1}Tmp1}}
   } {
    #If {@{History%{1}Tmp3}=""} {
     #Var {History%{1}Messages} {@{History%{1}Tmp1}}
    } {
     #AddItem {History%{1}Messages} {@{History%{1}Tmp1}}
    }
   };
   #If {@LogHistory=1} {
    #Math NumberOfBufferedLogMessages {@NumberOfBufferedLogMessages+1};
    #Exec {
     #If {~%Defined(History%{1}LogBufferEntries)=0} {
      #Var {History%{1}LogInitialization} 1;
      #Var {History%{1}LogBuffer} {};
      #Var {History%{1}LogBufferEntries} 0
     }
    };
    #Math {History%{1}LogBufferEntries} {@{History%{1}LogBufferEntries}+1};
    #Var {History%{1}LogBuffer} {@{History%{1}LogBuffer}%2%CRLF};
    #If {@NumberOfBufferedLogMessages>@HistoryLoggingInterval} {
     #Var NumberOfBufferedLogMessages 0;
     #Alarm 0 {WriteToLogFile}
    }
   }
  };
  #Exec {
   #UnVar {History%{1}Tmp1};
   #UnVar {History%{1}Tmp2};
   #UnVar {History%{1}Tmp3};
   #UnVar {History%{1}Tmp4}
  }
 }
} {}
#Alias Hist {#Exec {#Alarm 0 {History %0}}}
#Alias History {
 Speech Stop;
 #If {%1=""} {
  ConfPrint {Valid arguments are back, forward, copy, say, and switch.};
  #Abort
 };
 #Var HistoryTmp1 {%1*};
 #If {"say"=*@HistoryTmp1 or "copy"=*@HistoryTmp1} {
  #If {%2<>""} {
   #Var HistoryTmp2 {%2*};
   #ForAll {@HistoryBuffers} {
    #If {%i=*@HistoryTmp2} {
     #Var HistoryTmp2 {%i}
    }
   };
   #If {%Pos(*,@HistoryTmp2)=0} {
    #Exec {
     #If {~@HistoryTmp2<>"Output" and ~%Defined(History@{HistoryTmp2}Messages)=0} {
      #Say {The @HistoryTmp2 buffer is empty} NoDisplay;
      #Abort
     }
    };
    #If {%IsNumber(%3)=1} {
     #If {@HistoryTmp2="Output"} {
      #Var HistoryTmp3 10
     } {
      #Var HistoryTmp3 {%NumWords(@{History@{HistoryTmp2}Messages},|)}
     };
     #If {(%IsNumber(%4)=1 and %3<>%4) or (%4="+" and %3<@HistoryTmp3) or (%4="-" and %3>1)} {
      #If {%IsNumber(%4)=1} {
       #Var HistoryTmp4 {%4}
      } {
       #If {%4="-"} {
        #Var HistoryTmp4 {1}
       } {
        #Var HistoryTmp4 {@HistoryTmp3}
       }
      };
      #If {%Min(%3,@HistoryTmp4)<1 or %Max(%3,@HistoryTmp4)>@HistoryTmp3} {
       #Say {Valid range for the %Replace(@HistoryTmp2,0, ) buffer is 1 to @HistoryTmp3} NoDisplay;
       #Abort
      };
      #Var HistoryTmp5 {};
      #If {@HistoryTmp2="Output"} {
       #Var HistoryTmp6 {%Max(%3,@HistoryTmp4)};
       #While {@HistoryTmp6>=%Min(%3,@HistoryTmp4)} {
        #Var HistoryTmp5 {@HistoryTmp5%CRLF%LastLine(@HistoryTmp6)};
        #Math HistoryTmp6 {@HistoryTmp6-1}
       }
      } {
       #Math HistoryTmp6 {@HistoryTmp3+1-%Max(%3,@HistoryTmp4)};
       #Math HistoryTmp7 {@HistoryTmp3+1-%Min(%3,@HistoryTmp4)};
       #While {@HistoryTmp6<=@HistoryTmp7} {
        #Var HistoryTmp5 {@HistoryTmp5%CRLF%Word(@{History@{HistoryTmp2}Messages},|,@HistoryTmp6)};
        #Math HistoryTmp6 {@HistoryTmp6+1}
       };
       #Var HistoryTmp5 {%Replace(@HistoryTmp5,@CR@CR,|)}
      };
      #If {"say"=*@HistoryTmp1} {
       #Say {Message %Min(%3,@HistoryTmp4) to %Max(%3,@HistoryTmp4) in the %Replace(@HistoryTmp2,0, ) buffer:@HistoryTmp5}
      } {
       #Var Discard {%Clip(%Delete(@HistoryTmp5,1,2))};
       #UnVar Discard;
       #Say {Copied message %Min(%3,@HistoryTmp4) to %Max(%3,@HistoryTmp4) from the %Replace(@HistoryTmp2,0, ) buffer} NoDisplay
      }
     } {
      #If {@HistoryTmp2="Output"} {
       #If {%3<1 or %3>10} {
        #Var HistoryTmp1 {say};
        #Var HistoryTmp3 {Valid range for the Output buffer is 1 to 10}
       } {
        #Var HistoryTmp3 {%LastLine(%3)}
       }
      } {
       #Exec {
        #If {~%Defined(History@{HistoryTmp2}Messages)=1} {
         #If {%3<1 or %3>%NumWords(@{History@{HistoryTmp2}Messages},|)} {
          #Var HistoryTmp1 {say};
          #Var HistoryTmp3 {That message does not exist. Valid range for the %Replace(@HistoryTmp2,0, ) buffer is currently 1 to %NumWords(@{History@{HistoryTmp2}Messages},|)}
         } {
          #Var HistoryTmp3 {~%Word(~@{History@{HistoryTmp2}Messages},|,%Eval(%NumWords(@{History@{HistoryTmp2}Messages},|)+1-%3))};
          #Var HistoryTmp3 {~%Replace(~@HistoryTmp3,~@CR~@CR,|)}
         }
        } {
         #Var HistoryTmp1 {say};
         #Var HistoryTmp3 {No @HistoryTmp2 messages yet}
        }
       }
      };
      #If {"say"=*@HistoryTmp1} {
       #Say {@HistoryTmp3} NoDisplay
      } {
       #Var Discard {%Clip(@HistoryTmp3)};
       #UnVar Discard;
       #Say {Copied message: @HistoryTmp3} NoDisplay
      }
     }
    } {
     #If {@HistoryTmp2="Output"} {
      #Var HistoryTmp3 {};
      #If {"say"=*@HistoryTmp1} {
       #10 {#Var HistoryTmp3 {@HistoryTmp3%CRLF %LastLine(%Eval(11-%i))}}
      } {
       #10 {#Var HistoryTmp3 {@HistoryTmp3%CRLF%LastLine(%i)}}
      }
     } {
      #Exec {
       #If {~%Defined(History@{HistoryTmp2}Messages)=1} {
        #Var HistoryTmp3 {~%Replace(~@{History@{HistoryTmp2}Messages},|,~%CRLF )};
        #Var HistoryTmp3 {%CRLF ~%Replace(~@HistoryTmp3,~@CR~@CR,|)}
       } {
        #Say {The @HistoryTmp2 buffer is empty} NoDisplay;
        #Abort
       }
      }
     };
     #If {"say"=*@HistoryTmp1} {
      #Say {All content of the %Replace(@HistoryTmp2,0, ) buffer:@HistoryTmp3}
     } {
      #Var Discard {%Clip(%Delete(@HistoryTmp3,1,2))};
      #UnVar Discard;
      #Say {Copied the %Replace(@HistoryTmp2,0, ) buffer} NoDisplay
     }
    }
   } {
    #Say {That buffer does not exist} NoDisplay
   }
  } {
   #If {"say"=*@HistoryTmp1} {
    #Say {History buffers:%CRLF %Replace(%Replace(~@HistoryBuffers,0, ),|,~, )}
   } {
    #Var HistoryTmp3 {%Replace(%Replace(~@HistoryBuffers,0, ),|,~, )};
    #Var Discard {%Clip(@HistoryTmp3.)};
    #UnVar Discard;
    #Say {Copied list of history buffers} NoDisplay
   }
  };
  #Abort
 };
 #If {"switch"=*@HistoryTmp1} {
  #If {%2="back" or %2="forward"} {
   #Var HistoryTmp1 0;
   #Var HistoryTmp2 {%NumWords(@HistoryBuffers,|)};
   #While {@HistoryTmp1<@HistoryTmp2} {
    #Math HistoryTmp1 {@HistoryTmp1+1};
    #If {@CurrentHistoryBuffer=%Word(@HistoryBuffers,|,@HistoryTmp1)} {
     #If {%2="back"} {
      #If {@HistoryTmp1>1} {
       #Math HistoryTmp1 {@HistoryTmp1-1};
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,@HistoryTmp1)}
      } {
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,@HistoryTmp2)}
      }
     } {
      #If {@HistoryTmp1=@HistoryTmp2} {
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,1)}
      } {
       #Math HistoryTmp1 {@HistoryTmp1+1};
       #Var CurrentHistoryBuffer {%Word(@HistoryBuffers,|,@HistoryTmp1)}
      }
     };
     #Var HistoryTmp1 {@HistoryTmp2}
    }
   }
  } {
   #Var HistoryTmp1 {%2*};
   #ForAll {@HistoryBuffers} {
    #If {%i=*@HistoryTmp1} {
     #Var HistoryTmp1 {%i}
    }
   };
   #If {%Pos(*,@HistoryTmp1)=0} {
    #Var CurrentHistoryBuffer {@HistoryTmp1}
   } {
    #Say {That buffer does not exist} NoDisplay;
    #Abort
   }
  };
  #Say {%Replace(@CurrentHistoryBuffer,0, )} VoiceOnly;
  #Var CurrentHistoryBufferPosition 1;
  #Abort
 };
 #Exec {
  #If {~@CurrentHistoryBuffer<>"Output" and ~%Defined(History@{CurrentHistoryBuffer}Messages)=0} {
   #Say {No messages in the @CurrentHistoryBuffer buffer yet} NoDisplay;
   #Abort
  }
 };
 #If {"back"=*@HistoryTmp1} {
  #If {%2="all"} {
   #If {@CurrentHistoryBuffer="Output" and @HistoryBufferSize>10} {
    #Var CurrentHistoryBufferPosition 10
   } {
    #Var CurrentHistoryBufferPosition {%Min(%NumWords(@{History@{CurrentHistoryBuffer}Messages},|),@HistoryBufferSize)}
   };
   #Var HistoryTmp1 0
  } {
   #Math CurrentHistoryBufferPosition {@CurrentHistoryBufferPosition+1};
   #If {@CurrentHistoryBuffer="Output"} {
    #If {@CurrentHistoryBufferPosition>10} {
     #Var CurrentHistoryBufferPosition 10;
     #Var HistoryTmp1 1
    } {
     #Var HistoryTmp1 0
    }
   } {
    #If {@CurrentHistoryBufferPosition>%NumWords(@{History@{CurrentHistoryBuffer}Messages},|)} {
     #Var CurrentHistoryBufferPosition {%NumWords(@{History@{CurrentHistoryBuffer}Messages},|)};
     #Var HistoryTmp1 1
    } {
     #Var HistoryTmp1 0
    }
   };
   #If {@HistoryTmp1=1 and @Silent=0} {#Play {@SoundsPath\Ship\Computer\Beep@Ext} @Vol}
  };
  #Var HistoryTmp1 {}
 };
 #If {"forward"=*@HistoryTmp1} {
  #Math CurrentHistoryBufferPosition {@CurrentHistoryBufferPosition-1};
  #If {@CurrentHistoryBufferPosition<1 or %2="all"} {
   #If {@CurrentHistoryBufferPosition<1 and @Silent=0} {#Play {@SoundsPath\Ship\Computer\Beep@Ext} @Vol};
   #Var CurrentHistoryBufferPosition 1
  };
  #Var HistoryTmp1 {}
 };
 #If {@HistoryTmp1<>""} {
  #Say {I don't recognize that argument} NoDisplay;
  #Abort
 };
 #If {@CurrentHistoryBuffer="Output"} {
  #Say {%LastLine(@CurrentHistoryBufferPosition)} NoDisplay;
  #If {@PrintVoiceOnly=1} {#Say {[Review] %LastLine(@CurrentHistoryBufferPosition)} NoVoice}
 } {
  #Var HistoryTmp1 {%Word(@{History@{CurrentHistoryBuffer}Messages},|,%Eval(%NumWords(@{History@{CurrentHistoryBuffer}Messages},|)+1-@CurrentHistoryBufferPosition))};
  #Var HistoryTmp1 {%Replace(@HistoryTmp1,@CR@CR,|)};
  #If {@CurrentHistoryBuffer="Played0Sounds" and @PlaySoundsOnReview=1 and @Silent=0} {
   #If {%Pos(:,@HistoryTmp1)>0} {
    #Var HistoryTmp2 {%Pos(:,@HistoryTmp1)};
    #Math HistoryTmp2 {@HistoryTmp2+8};
    #Var HistoryTmp3 {%Pos(" at ",@HistoryTmp1)};
    #If {%NumWords(@HistoryTmp1,:)>2} {
     #Var HistoryTmp4 {}
    } {
     #Var HistoryTmp4 {@SoundsPath\}
    };
    #If {@HistorySoundHandle<>0} {
     #PC @HistorySoundHandle Stop
    };
    #Play {@HistoryTmp4%Copy(@HistoryTmp1,%Eval(@HistoryTmp2+2),%Eval(@HistoryTmp3-(@HistoryTmp2+2)))} {%Min(%Eval(@Vol+@HistoryVolumeOffset),100)};
    #Var HistorySoundHandle %PlayHandle;
    #PC @HistorySoundHandle Pan @HistoryPanning
   }
  };
  #Say {@HistoryTmp1} NoDisplay;
  #If {@PrintVoiceOnly=1} {
   #Say {[Review] @HistoryTmp1} NoVoice
  }
 }
} {}

#Alias PickHistoryBuffer {
 #Var SelectedBuffer {%Word(@HistoryBuffers,|,1)};
 SetVariable HistorySelectionTmp1 {P:Select Buffer};
 SetVariable HistorySelectionTmp2 {};
 #ForAll @HistoryBuffers {
  #Var HistorySelectionTmp2 {%Replace(%I,0, )};
  #AddItem HistorySelectionTmp1 {@HistorySelectionTmp2:#Var SelectedBuffer %i}
 };
 #Pick @HistorySelectionTmp1
} {}

#Key {Shift-Control-Alt-F11} {BufferConfig} {}
